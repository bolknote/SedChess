#n
1s/.*/\
    Bin:1\
    figures!\
    board!\
    repeat?\
    estimate-black-pieces!\
    log!\
    del!\
    input!\
    step!\
    board!\
    count-pieces!\
    repeat-end!\
/

    # estimate-black-queen!\
    # estimate-black-pawn!\
    # estimate-black-king!\
    # estimate-black-bishop!\
    # estimate-black-queen!\

# –ø–µ—Ä–µ—Ñ–æ—Ä–º–∞—Ç–∏—Ä–æ–≤–∞–Ω–∏–µ –∫–æ–º–∞–Ω–¥
1s/ *//g; 1s/\n/ /g; 1s/^ //

# –æ–±—Ä–∞–±–æ—Ç–∫–∞ –ø–æ—Å—Ç—É–ø–∞—é—â–µ–π –∫–æ–º–∞–Ω–¥—ã
1!{
    /^[a-h][1-8] *[a-h][1-8]$/ {
        # –¥–æ–±–∞–≤–ª—è–µ–º –ø–æ–ª—É—á–µ–Ω–Ω—ã–µ –∑–Ω–∞—á–µ–Ω–∏—è –≤–ø–µ—Ä–µ–¥–∏ —Å—Ç–µ–∫–∞ –∏—Å–ø–æ–ª–Ω–µ–Ω–∏—è
        G; s/\n/ /
        # –ø–µ—Ä–µ—Ö–æ–¥–∏–º –Ω–∞ –∏—Å–ø–æ–ª–Ω–µ–Ω–∏–µ –∫–æ–º–∞–Ω–¥
        b @
    }

    # –∏–≥—Ä–æ–∫ —Ö–æ—á–µ—Ç –≤—ã–π—Ç–∏
    /^q/ q

    # –≤–≤–µ–¥–µ–Ω–∞ –∫–∞–∫–∞—è-—Ç–æ –µ—Ä—É–Ω–¥–∞, —Å—Ç–∏—Ä–∞–µ–º –∏ –≤–æ–∑–≤—Ä–∞—â–∞–µ–º —Å—Ç–µ–∫ –∫–æ–º–∞–Ω–¥
    i\
    [12H[J[1A
    s/.*//

    g
    b
}

:@

/[a-z][a-z-]*[!?]/ {
    s//@&/

    # —É–±—Ä–∞—Ç—å –≤–µ—Ä—Ö–Ω–µ–µ –∑–Ω–∞—á–µ–Ω–∏–µ
    /@del! */ {
        s///
        s/^[^ ]* *//
        b @
    }

    # –ø–æ–≤—Ç–æ—Ä, –ø–æ–∫–∞ –Ω–∞ –≤–µ—Ä—à–∏–Ω–µ –Ω–µ –ø—É—Å—Ç–æ–µ
    /@repeat\? */ {
        /Bin:1* */ {
            s///
            s/@repeat\? \(.*\)repeat-end!/\1repeat\? \1repeat-end!/

            b @
        }
        s/Bin:[^ ]* *//

        b @
    }

    # –¥—É–±–ª–∏—Ä–æ–≤–∞–Ω–∏–µ –ø–µ—Ä–≤–æ–≥–æ –∑–Ω–∞—á–µ–Ω–∏—è
    /@dup! */ {
        s///
        s/[^ ]*/& &/

        b @
    }

    # –≤–≤–æ–¥ –¥–∞–Ω–Ω—ã—Ö
    /@input! */ {
        s///; h; b
    }

    # –≥–µ–Ω–µ—Ä–∞—Ü–∏—è –Ω–∞—á–∞–ª—å–Ω–æ–≥–æ —Å–æ—Å—Ç–æ—è–Ω–∏—è –¥–æ—Å–∫–∏
    /@figures! */ {
        # —Ñ–æ—Ä–º–∞—Ç: XYFig
        # –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã –±–µ–ª—ã—Ö —Ç—É—Ç –∏ –¥–∞–ª—å—à–µ –¥–æ–ª–∂–Ω—ã –∏–¥—Ç–∏ –ù–ò–ñ–ï —á—ë—Ä–Ω—ã—Ö
        # –ë–û–õ–¨–®–ò–ï ‚Äî —á—ë—Ä–Ω—ã–µ, –º–∞–ª–µ–Ω—å–∫–∏–µ¬†‚Äî –±–µ–ª—ã–µ
        s//Board:\
a8Rb8Nc8Id8Qe8Kf8Ig8Nh8R\
a7Pb7Pc7Pd7Pe7Pf7Pg7Ph7P\
a6¬†b6¬†c6¬†d6¬†e6¬†f6¬†g6¬†h6¬†\
a5¬†b5¬†c5¬†d5¬†e5¬†f5¬†g5¬†h5¬†\
a4¬†b4¬†c4¬†d4¬†e4¬†f4¬†g4¬†h4¬†\
a3¬†b3¬†c3¬†d3¬†e3¬†f3¬†g3¬†h3¬†\
a2pb2pc2pd2pe2pf2pg2ph2p\
a1rb1nc1id1qe1kf1ig1nh1r /
# –ø—Ä–æ–±–µ–ª –≤ –∫–æ–Ω—Ü–µ –Ω—É–∂–µ–Ω!

        s/\n//g

        b @
    }

    # –≤—ã–≤–æ–¥ –¥–æ—Å–∫–∏
    /@board! */ {
        s///
        # —Å–æ—Ö—Ä–∞–Ω—è–µ–º —Å—Ç–µ–∫ –∫–æ–º–∞–Ω–¥
        h
        # —É–±–∏—Ä–∞–µ–º –≤—Å—ë, –∫—Ä–æ–º–µ –¥–æ—Å–∫–∏
        s/.*Board://
        s/ .*$//
        # —Ä–∞—Å—à–∏—Ñ—Ä–æ–≤—ã–≤–∞–µ–º –¥–æ—Å–∫—É
        # Pawn, Queen, King, bIshop, kNight, Rook
        y/pqkinrPQKINR12345678abcd/‚ôü‚ôõ‚ôö‚ôù‚ôû‚ôú‚ôô‚ôï‚ôî‚ôó‚ôò‚ôñ987654323579/
        s/\([1-9e-h]\)\([1-9]\)\(.\)/[\2;\1H\3 /g

        # —Ä–∞—Å—Ü–≤–µ—á–∏–≤–∞–µ–º
        s/[8642];[37eg]H/&[48;5;209;37;1m/g
        s/[9753];[37eg]H/&[48;5;94;37;1m/g
        s/[8642];[59fh]H/&[48;5;94;37;1m/g
        s/[9753];[59fh]H/&[48;5;209;37;1m/g

        # –¥–≤—É–∑–Ω–∞—á–Ω—ã–µ —á–∏—Å–ª–∞
        s/e/11/g;s/f/13/g;s/g/15/g;s/h/17/g

        s/$/[0m[11H/
        # –≤—ã–≤–æ–¥–∏–º –¥–æ—Å–∫—É –∏ –≤–æ–∑–≤—Ä–∞—â–∞–µ–º –≤—Å—ë –∫–∞–∫ –±—ã–ª–æ
        i\
[2J[1;3Ha b c d e f g h\
8\
7\
6\
5\
4\
3\
2\
1\
\
Enter command:
        p
        g

        b @
    }

    # –¥–µ–ª–∞–µ–º —Ö–æ–¥ –ø–æ –≤–≤–µ–¥—ë–Ω–Ω—ã–º –ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª–µ–º –¥–∞–Ω–Ω—ã–º
    /@step! */ {
        s///

        # –≥–∞—Ä–¥—ã –æ—Å–Ω–æ–≤–Ω—ã—Ö —Ä–µ–≥—É–ª—è—Ä–æ–∫ (–∏—Ö –Ω—É–∂–Ω–æ —Ç—â–∞—Ç–µ–ª—å–Ω–æ –∑–∞—â–∏—â–∞—Ç—å –æ—Ç –Ω–µ—Å—Ä–∞–±–∞—Ç—ã–≤–∞–Ω–∏–π,
        # –∏–Ω–∞—á–µ sed –≤—ã–¥–∞—Å—Ç –æ—à–∏–±–∫—É –∏ –æ—Å—Ç–∞–Ω–æ–≤–∏—Ç—Å—è)
        # –≤—ã—á–∏—â–∞–µ–º –≤—Å—ë, –∫—Ä–æ–º–µ –¥–æ—Å–∫–∏ –∏ –ø–µ—Ä–≤—ã—Ö –¥–≤—É—Ö –∑–Ω–∞—á–µ–Ω–∏–π
        h; s/\([^ ]*\) \([^ ]*\).*Board:\([^ ]*\).*/\1 \2 \3/
        
        # –≤—ã–¥–µ–ª—è–µ–º —É–∫–∞–∑–∞–Ω–Ω—ã–µ –∫–ª–µ—Ç–∫–∏
        s/\([^ ]*\) [^ ]* .*\(\1.\)/&(1:\2)/
        s/[^ ]* \([^ ]*\) .*\(\1.\)/&(2:\2)/
        # —Ç–µ–ø–µ—Ä—å –æ–Ω–∏ –∏–º–µ—é—Ç —Ñ–æ—Ä–º–∞—Ç:
        # –Ω–æ–º–µ—Ä_–ø–æ_–ø–æ—Ä—è–¥–∫—É_–≤–≤–æ–¥–∞:XY–§–∏–≥—É—Ä–∞
        s/.*(\(.....\)).*(\(.....\)).*/\1 \2/

        # —Ç–µ–ø–µ—Ä—å –Ω–∞–¥–æ –ø—Ä–æ–≤–µ—Ä–∏—Ç—å:
        # 1. —á—Ç–æ –±–µ—Ä—ë–º –Ω–µ —á—É–∂—É—é –∏ –Ω–µ –ø—É—Å—Ç—É—é —Ñ–∏–≥—É—Ä—É
        /1:..[PQKINR¬†]/ {
            g; s/[^ ]* [^ ]* *//; b @
        }

        # 2. –Ω–µ –∫–ª–∞–¥—ë–º –Ω–∞ –º–µ—Å—Ç–æ —Å–≤–æ–µ–π —Ñ–∏–≥—É—Ä—ã
        /2:..[pqkbnr]/ {
            g; s/[^ ]* [^ ]* *//; b @
        }

        # –µ—Å–ª–∏ —Ö–æ–¥ –±—É–¥–µ—Ç –≤–ø–µ—Ä—ë–¥‚Ä¶
        /2:.*1:/ {
            g
            /\([^ ]*\) \([^ ]*\) \(.*Board:[^ ]*\2\).\([^ ]*\1\)\([pqkbnr]\)/ {
                s//\3\5\4¬†/
                b @
            }
        }

        # —Ö–æ–¥ –Ω–∞–∑–∞–¥
        g
        s/\([^ ]*\) \([^ ]*\) \(.*Board:[^ ]*\1\)\([pqkbnr]\)\([^ ]*\2\)./\3¬†\5\4/
        b @
    }

    # –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ –æ—Å—Ç–∞–≤—à–∏—Ö—Å—è —Ñ–∏–≥—É—Ä
    /@count-pieces! */ {
        s///
        h
        # —É–±–∏—Ä–∞–µ–º –≤—Å—ë, –∫—Ä–æ–º–µ –¥–æ—Å–∫–∏
        s/.*Board://
        s/ .*$//
        # —É–±–∏—Ä–∞–µ–º –≤—Å—ë, –∫—Ä–æ–º–µ –±–µ–ª—ã—Ö —Ñ–∏–≥—É—Ä
        s/[^pqkbnrPQKINR]//g
        # —Å—á–∏—Ç–∞–µ–º
        s/./1/g; s/^/Bin:/
        # –≤–æ–∑–≤—Ä–∞—â–∞–µ–º —Å—Ç–µ–∫ –∫–æ–º–∞–Ω–¥
        G
        # –ø–æ—Å–ª–µ G –ø–æ—è–≤–∏–ª—Å—è –ø–µ—Ä–µ–≤–æ–¥ —Å—Ç—Ä–æ–∫–∏, —É–±–∏—Ä–∞–µ–º –µ–≥–æ
        s/\n/ /

        b @
    }

    #–æ—Ü–µ–Ω–æ—á–Ω–∞—è —Ñ—É–Ω–∫—Ü–∏—è –∏–º–µ—é—â–∏—Ö—Å—è —á—ë—Ä–Ω—ã—Ö —Ñ–∏–≥—É—Ä
    /@estimate-black-pieces! */ {
        # –ø–µ—à–∫–∞¬†‚Äî 100, —Å–ª–æ–Ω –∏ –∫–æ–Ω—å¬†‚Äî 300, –ª–∞–¥—å—è¬†‚Äî 500, —Ñ–µ—Ä–∑—å¬†‚Äî 900

        # –æ—á–∏—Å—Ç–∫–∞ –≤—Å–µ–≥–æ –ª–∏—à–Ω–µ–≥–æ
        s///; h; s/.*Board://; s/ .*$//
        # —É–±–∏—Ä–∞–µ–º –≤—Å—ë, –∫—Ä–æ–º–µ –ø–æ–¥—Å—á–∏—Ç—ã–≤–∞–µ–º—ã—Ö —Ñ–∏–≥—É—Ä
        s/[^PINRQ]//g
        # —Å—á–∏—Ç–∞–µ–º –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ * –∫–æ—ç—Ñ—Ñ–∏—Ü–∏–µ–Ω—Ç —Ñ–∏–≥—É—Ä—ã (—Ñ–µ—Ä–∑—å¬†Q ‚Äî –µ–¥–∏–Ω—Å—Ç–≤–µ–Ω–Ω—ã–π)
        s/P/1/g; s/[IN]/111/g; s/R/11111/g; s/Q/111111111/

        # –≥—Ä—É–ø–ø–∏—Ä—É–µ–º —Å–æ—Ç–Ω–∏ –∏ —Ç—ã—Å—è—á–∏
        s/1111111111/H/g; s/HHHHHHHHHHH/T/g; s/\(.\)\1*/&:/g; s/[ :]*$/::/; y/HT/11/

        # –¥–æ–±–∞–≤–ª—è–µ–º –∫ —Å–æ—Ö—Ä–∞–Ω—ë–Ω–Ω–æ–º—É —Å—Ç–µ–∫—É
        s/^/Bin:/; G; s/\n/ /

        b @
    }

    #–¥–ª—è –æ—Ç–ª–∞–¥–∫–∏: –≤—ã–≤–æ–¥ —Ç–µ–∫—É—â–µ–≥–æ —Å—Ç–µ–∫–∞
    /@log! */ {
        s///
        l
        b @
    }

    #–æ—Ü–µ–Ω–æ—á–Ω–∞—è —Ñ—É–Ω–∫—Ü–∏—è –¥–ª—è –ø–æ–∑–∏—Ü–∏–∏ —á—ë—Ä–Ω—ã—Ö –ø–µ—à–µ–∫
    /@estimate-black-pawn! */ {
        # –æ—á–∏—Å—Ç–∫–∞ –≤—Å–µ–≥–æ –ª–∏—à–Ω–µ–≥–æ
        s///; h; s/.*Board://; s/ .*$//
        # –æ—Å—Ç–∞–≤–ª—è–µ–º —Ç–æ–ª—å–∫–æ —á—ë—Ä–Ω—ã–µ –∏ –±–µ–ª—ã–µ –ø–µ—à–∫–∏, –ø–µ—Ä–µ–∫–æ–¥–∏—Ä—É–µ–º –∏—Ö –≤ –ø–æ–Ω—è—Ç–Ω—ã–µ –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã
        # —Ç–µ–ø–µ—Ä—å –ø–µ—à–∫–∏ –∑–∞–ø–∏—Å–∞–Ω—ã –≤–æ—Ç —Ç–∞–∫: X–¶–≤–µ—Ç (–≥–¥–µ –¶–≤–µ—Ç ‚Äî Black –∏–ª–∏ White), —Ä–∞–∑–¥–µ–ª–µ–Ω—ã –ø—Ä–æ–±–µ–ª–æ–º
        s/[a-h][1-8][^Pp]//g; y/Ppabcdefgh/WB12345678/; s/\([1-8]\)[1-8]/ \1/g

        # ‚Üí –≠—Ç–∞–ø 1
        # –∏—â–µ–º —á—ë—Ä–Ω—ã–µ –ø–µ—à–∫–∏, –Ω–∞ –≤–µ—Ä—Ç–∏–∫–∞–ª–∏ —É –∫–æ—Ç–æ—Ä—ã—Ö —Å—Ç–æ—è—Ç –±–µ–ª—ã–µ, –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã –±–µ–ª—ã—Ö –∏–¥—É—Ç
        # –≤—Å–µ–≥–¥–∞ –ü–ï–†–ï–î –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç–∞–º–∏ —á—ë—Ä–Ω—ã—Ö
        :estimate-black-pawn::black
        /\([1-8]\)W\(.*\1\)B/ {
            s//\1W\2b/
            b estimate-black-pawn::black
        }

        # ‚Üí –≠—Ç–∞–ø 2.1
        # –ø–µ—Ä–µ–≤–æ–¥–∏–º –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã –≤ –ø–æ—Å–ª–µ–¥–æ–≤–∞—Ç–µ–ª—å–Ω–æ—Å—Ç–∏ –¥–ª–∏–Ω—ã X
        :estimate-black-pawn::x
        /[2-8]/ {
            s/[2-8]/1&/g
            y/2345678/1234567/

            b estimate-black-pawn::x
        }

        # ‚Üí –≠—Ç–∞–ø 2.2
        # –∏—â–µ–º –ø–µ—à–∫–∏, –Ω–µ –æ—Ç—Å–µ—è–Ω–Ω—ã–µ –Ω–∞ —ç—Ç–∞–ø–µ 1, —É –∫–æ—Ç–æ—Ä—ã—Ö –Ω–∞ —Å–æ—Å–µ–¥–Ω–µ–π –ª–∏–Ω–∏–∏ —Å–ª–µ–≤–∞ —Å—Ç–æ—è—Ç –±–µ–ª—ã–µ
        :estimate-black-pawn::left
        /\( 1*\)W\(.*\11\)B/ {
            s//\1W\2b/
            b estimate-black-pawn::left
        }

        # ‚Üí –≠—Ç–∞–ø 2.3
        # –∏—â–µ–º –ø–µ—à–∫–∏, –Ω–µ –æ—Ç—Å–µ—è–Ω–Ω—ã–µ –Ω–∞ —ç—Ç–∞–ø–µ 2, —É –∫–æ—Ç–æ—Ä—ã—Ö –Ω–∞ —Å–æ—Å–µ–¥–Ω–µ–π –ª–∏–Ω–∏–∏ —Å–ø—Ä–∞–≤–∞ —Å—Ç–æ—è—Ç –±–µ–ª—ã–µ
        :estimate-black-pawn::right
        / 1\(1*\)W\(.* \1\)B/ {
            s// 1\1W\2b/
            b estimate-black-pawn::right
        }

        # –í –∏—Ç–æ–≥–µ, W¬†‚Äî –±–µ–ª—ã–µ –ø–µ—à–∫–∏, b¬†‚Äî —á—ë—Ä–Ω—ã–µ, B¬†‚Äî —á—ë—Ä–Ω—ã–µ —Å–≤–æ–±–æ–¥–Ω—ã–µ –ø–µ—à–∫–∏
        # –∏–∑–±–∞–≤–ª—è–µ–º—Å—è –æ—Ç –Ω–µ—Å–≤–æ–±–æ–¥–Ω—ã—Ö –∏ –±–µ–ª—ã—Ö –ø–µ—à–µ–∫
        s/ [^ ]*[Wb]//g

        # ‚Üí –≠—Ç–∞–ø 3
        # —Å—á–∏—Ç–∞–µ–º —Å—Ç–æ–∏–º–æ—Å—Ç–∏ —á—ë—Ä–Ω—ã—Ö —Å–≤–æ–±–æ–¥–Ω—ã—Ö –ø–µ—à–µ–∫
        s/ 1B//; s/ 11B/ ::11111B/; s/ 111B/ :1:B/; s/ 1111B/ :1:11111B/; s/ 11111B/ :11:B/
        s/ 111111B/ :111:B/; s/ 1111111B/ 1:1111:B/; s/ 11111111B//

        # ‚Üí –≠—Ç–∞–ø 4
        # —Å–æ—Ö—Ä–∞–Ω—è–µ–º –ø–æ–ª—É—á–µ–Ω–Ω–æ–µ, –≥—Ä—É–∑–∏–º —Å—Ç–µ–∫ –æ–±—Ä–∞—Ç–Ω–æ, –≤—ã—Ä–µ–∑–∞–µ–º –¥–æ—Å–∫—É –∏ –æ—Å—Ç–∞–≤–ª—è–µ–º —á—ë—Ä–Ω—ã–µ –ø–µ—à–∫–∏ —Å –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç–∞–º–∏
        G; h; s/.*Board://; s/ .*$//; s/[a-h][1-8][^p]//g

        # –æ—Ü–µ–Ω–∏–≤–∞–µ–º –ø–æ–∑–∏—Ü–∏–∏ –≤—Å–µ—Ö –ø–µ—à–µ–∫
        s/.[81]p/::B/g

        s/[abcfgh]7p/::1111B/g; s/[de]7p/::B/g

        s/[ah][65]p/::111111B/g; s/[bg][65]p/::11111111B/g; s/[cf]6p/::11B/g; s/[de]6p/:1:B/g

        s/[bg]5p/:1:11B/g; s/[cf]5p/:1:111111B/g; s/[de]5p/:11:1111B/g

        s/[ah]4p/::11111111B/g; s/[bg]4p/:1:11B/g; s/[cf]4p/:1:111111B/g; s/[de]4p/:11:1111B/g

        s/[ah][32]p/:1:11B/g; s/[bg][32]p/:1:111111B/g; s/[cf][32]p/:11:1111B/g; s/[de][32]p/:111:11B/g

        # –≤—Å—Ç–∞–≤–ª—è–µ–º –ø—Ä–æ–±–µ–ª—ã –º–µ–∂–¥—É –æ—Ü–µ–Ω–∫–∞–º–∏
        s/B/& /g; s/^/ /

        # ‚Üí –≠—Ç–∞–ø 5
        # –≤–æ–∑–≤—Ä–∞—â–∞–µ–º —Å–æ—Ö—Ä–∞–Ω—ë–Ω–Ω—ã–µ –æ—Ü–µ–Ω–∫–∏, —É–±–∏—Ä–∞–µ–º –æ—Å—Ç–∞—Ç–∫–∏ —Å—Ç–µ–∫–∞
        G; s/\n\(.*\)\n.*/ \1/

        # ‚Üí –≠—Ç–∞–ø 6
        # —Ç–µ–ø–µ—Ä—å —É –Ω–∞—Å –µ—Å—Ç—å –ø–æ–∑–∏—Ü–∏–æ–Ω–Ω—ã–µ –æ—Ü–µ–Ω–∫–∏ —Å–≤–æ–±–æ–¥–Ω—ã—Ö —á—ë—Ä–Ω—ã—Ö –ø–µ—à–µ–∫ –∏ –æ—Ü–µ–Ω–∫–∏ –≤—Å–µ—Ö —á—ë—Ä–Ω—ã—Ö –ø–µ—à–µ–∫
        # –Ω—É–∂–Ω–æ –∏—Ö —Å–ª–æ–∂–∏—Ç—å
        s/$/ :::S/

        # FIXME!!!!         # –°–£–ú–ú–ê –ù–ï –†–ê–ë–û–¢–ê–ï–¢ –° –ü–†–û–ü–£–°–ö–ê–ú–ò –†–ê–ó–†–Ø–î–û–í!!

        :estimate-black-pawn::shift
        /11*B/ {
            # —Å–ª–æ–∂–µ–Ω–∏–µ —Ä–∞–∑—Ä—è–¥–∞
            :estimate-black-pawn::sum
            /11*B/ {
                s/\(11*\)B\(.*\)\(1*\)S/B\2\1\3S/
                s/:1111111111\(1*\)S/1:\1S/

                b estimate-black-pawn::sum
            }

            # —Å–¥–≤–∏–≥ —Ä–∞–∑—Ä—è–¥–∞
            s/:B/B/g; s/:\(1*\)S/S \1:/

            b estimate-black-pawn::shift
        }

        s/:\(1*\)S/S \1:/

        # –Ω–æ—Ä–º–∞–ª–∏–∑–∞—Ü–∏—è —á–∏—Å–ª–∞: —Å–æ—Ç–Ω–∏:–¥–µ—Å—è—Ç–∫–∏:–µ–¥–∏–Ω–∏—Ü—ã
        # –Ω–∞ —ç—Ç–æ–º —ç—Ç–∞–ø–µ –Ω–µ–æ—Ç–∫—É–¥–∞ –ø–æ—è–≤–∏—Ç—å—Å—è —Ç—ã—Å—è—á–∞–º¬†‚Äî –º–∞–∫—Å–∏–º–∞–ª—å–Ω–∞—è —Å—É–º–º–∞ 388
        s/[^:1]//g; s/:$//; s/^/Bin:/

        # –¥–æ–±–∞–≤–ª—è–µ–º –∫ —Å–æ—Ö—Ä–∞–Ω—ë–Ω–Ω–æ–º—É —Å—Ç–µ–∫—É, –≤—ã—á–∏—â–∞–µ–º –Ω–∞—à –º—É—Å–æ—Ä, –∫–æ—Ç–æ—Ä—ã–π –º—ã —Å–∫–ª–∞–¥—ã–≤–∞–ª–∏ –≤—ã—à–µ¬†‚Äî
        # —Ç–∞–º –≤—Ç–æ—Ä–æ–π —Å—Ç—Ä–æ–∫–æ–π –ª–µ–∂–∞—Ç –æ—Ü–µ–Ω–∫–∏
        G; s/\n.*\n/ /

        b @
    }

    #–æ—Ü–µ–Ω–æ—á–Ω–∞—è —Ñ—É–Ω–∫—Ü–∏—è –¥–ª—è –ø–æ–∑–∏—Ü–∏–∏ —á—ë—Ä–Ω–æ–≥–æ –∫–æ—Ä–æ–ª—è
    /@estimate-black-king! */ {
        s///; h; s/.*Board://; s/ .*$//

        # –≤—ã–¥–µ–ª—è–µ–º –∫–æ—Ä–æ–ª—è
        s/[a-h][1-8][^k]//g

        # —Å—á–∏—Ç–∞–µ–º –µ–≥–æ –≤–µ—Å (–º–∞—Ç—Ä–∏—Ü–∞ –∫–æ–Ω—Ü–∞ –∏–≥—Ä—ã)
        s/[ah][18]./::/
        
        s/[de][54]./:111:111111/
        
        s/[cf][54]./:111:/; s/[de][63]./:111:/

        s/[bg][54]./:11:1111/; s/[de][72]./:11:1111/; s/[cf][63]./:11:1111/

        s/[de][18]./:1:11111111/; s/[ah][54]./:1:11111111/; s/[cf][72]./:1:11111111/; s/[bg][63]./:1:11111111/

        s/[bg][72]./:1:11/; s/[ah][63]./:1:11/; s/[cf][81]./:1:11/

        s/[a-h][1-9]./::111111/

        s/^/Bin:/; G; s/\n/ /

        b @
    }

    #–æ—Ü–µ–Ω–æ—á–Ω–∞—è —Ñ—É–Ω–∫—Ü–∏—è –¥–ª—è –ø–æ–∑–∏—Ü–∏–∏ —á—ë—Ä–Ω–æ–≥–æ –∫–æ–Ω—è
    /@estimate-black-knight! */ {
        s///; h; s/.*Board://; s/ .*$//

        # –≤—ã–¥–µ–ª—è–µ–º –∫–æ–Ω–µ–π
        s/[a-h][1-8][^n]//g

        # —Å—á–∏—Ç–∞–µ–º –∏—Ö –≤–µ—Å
        s/[ah][18]./::B/g
        
        s/[de][54]./:111:11B/g
        
        s/[cf][54]./:11:11111111B/g; s/[de][63]./:11:11111111B/g

        s/[cf][36]./:11:1111B/g

        s/[bg][54]./:11:B/g; s/[de][72]./:11:B/g; s/[cf][63]./:11:B/g

        s/[de][18]./:1:B/g; s/[ah][54]./:1:B/g; s/[cf][72]./:1:B/g; s/[bg][63]./:1:B/g

        s/[bg][72]./::11111111B/g; s/[ah][63]./::11111111B/g; s/[cf][81]./::11111111B/g

        s/[a-h][1-9]./::1111B/

        # —Å–∫–ª–∞–¥—ã–≤–∞–µ–º –≤–µ—Å–∞
        :estimate-black-knight::shift
        /11*B/ {
            :estimate-black-knight::sum
            /11*B/ {
                s/\(11*\)B\(.*\)\(1*\)S/B\2\1\3S/; s/:1111111111\(1*\)S/1:\1S/
                b estimate-black-knight::sum
            }
            s/:B/B/g; s/:\(1*\)S/S \1:/
            b estimate-black-knight::shift
        }

        s/^/Bin:/; G; s/\n/ /

        b @
    }

    #–æ—Ü–µ–Ω–æ—á–Ω–∞—è —Ñ—É–Ω–∫—Ü–∏—è –¥–ª—è –ø–æ–∑–∏—Ü–∏–∏ —á—ë—Ä–Ω–æ–≥–æ —Å–ª–æ–Ω–∞
    /@estimate-black-bishop! */ {
        s///; h; s/.*Board://; s/ .*$//

        # –≤—ã–¥–µ–ª—è–µ–º —Å–ª–æ–Ω–æ–≤
        s/[a-h][1-8][^i]//g

        # —Å—á–∏—Ç–∞–µ–º –∏—Ö –≤–µ—Å
        s/[a-h][81]./:1:1111B/g; s/[ah][1-8]./:1:1111B/g

        s/[bg][72]./:11:11B/g; s/[c-f][3-6]/:11:11B/g

        s/[a-h][1-9]./:1:11111111B/g

        # —Å–∫–ª–∞–¥—ã–≤–∞–µ–º –≤–µ—Å–∞
        :estimate-black-bishop::shift
        /11*B/ {
            :estimate-black-bishop::sum
            /11*B/ {
                s/\(11*\)B\(.*\)\(1*\)S/B\2\1\3S/; s/:1111111111\(1*\)S/1:\1S/
                b estimate-black-bishop::sum
            }
            s/:B/B/g; s/:\(1*\)S/S \1:/
            b estimate-black-bishop::shift
        }

        s/^/Bin:/; G; s/\n/ /

        b @
    }

    #–æ—Ü–µ–Ω–æ—á–Ω–∞—è —Ñ—É–Ω–∫—Ü–∏—è –¥–ª—è –ø–æ–∑–∏—Ü–∏–∏ —á—ë—Ä–Ω–æ–π –∫–æ—Ä–æ–ª–µ–≤—ã (—Ñ–µ—Ä–∑—è)
    /@estimate-black-queen! */ {
        s///; h; s/.*Board://; s/ .*$//

        # –≤—ã–¥–µ–ª—è–µ–º —Ñ–µ—Ä–∑—è –∏ –≤—Ä–∞–∂–µ—Å–∫–æ–≥–æ –∫–æ—Ä–æ–ª—è
        s/[a-h][1-8][^qK]//g

        # –µ—Å–ª–∏ –æ–¥–Ω–æ–π –∏–∑ —Ñ–∏–≥—É—Ä –Ω–∞ –ø–æ–ª–µ –Ω–µ—Ç, –≤–æ–∑–≤—Ä–∞—Ç
        /....../ ! {
            g; b @
        }

        # —Ñ–∏–≥—É—Ä—ã —É–±–∏—Ä–∞–µ–º, –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã –∫ —á–∏—Å–ª–∞–º
        y/abcdefgh/12345678/; s/\([1-9]\)\(.\)./\1 \2 /g

        # –≥—Ä—É–ø–ø–∏—Ä—É–µ–º –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã, –ø–æ–ª—É—á–∏—Ç—Å—è X1 X2 Y1 Y2
        s/\([^ ]\) \([^ ]\) \([^ ]\)/\1 \3 \2/

        # –ø–µ—Ä–µ–≤–æ–¥–∏–º –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã –≤ –ø–æ—Å–ª–µ–¥–æ–≤–∞—Ç–µ–ª—å–Ω–æ—Å—Ç–∏ –¥–ª–∏–Ω—ã –∑–Ω–∞—á–µ–Ω–∏–π –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç
        :estimate-black-queen::xy
        /[2-8]/ {
            s/[2-8]/1&/g
            y/2345678/1234567/

            b estimate-black-queen::xy
        }        
        # —Å–æ—Ä—Ç–∏—Ä–æ–≤–∫–∞¬†‚Äî –±–æÃÅ–ª—å—à–∞—è –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç–∞ –≤–ø–µ—Ä—ë–¥
        s/\(11*\) \(11*\1\)/\2 \1/g

        # –≤—ã—á–∏—Ç–∞–µ–º –≤—Ç–æ—Ä—É—é –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—É –∏–∑ –ø–µ—Ä–≤–æ–π
        s/\(11*\)\(1*\) \1/\2/g

        # —É–º–Ω–æ–∂–∞–µ–º Y-–∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—É –Ω–∞ 8
        :estimate-black-queen::mul8
        / 1/ {
            s//88888888 /g
            b estimate-black-queen::mul8
        }
        y/8/1/

        # —É–º–Ω–æ–∂–∞–µ–º –ø–æ–ª—É—á–∏–≤—à–∏–π—Å—è –∫–æ—ç—Ñ—Ñ–∏—Ü–∏–µ–Ω—Ç –Ω–∞ 4
        s/1/1111/g
        # –≥—Ä—É–ø–ø–∏—Ä—É–µ–º –¥–µ—Å—è—Ç–∫–∏ –∏ —Å–æ—Ç–Ω–∏, —Ç—ã—Å—è—á–∏ –Ω–µ –Ω—É–∂–Ω—ã, –º–∞–∫—Å–∏–º–∞–ª—å–Ω–∞—è –æ—Ü–µ–Ω–∫–∞ ‚Äî
        # –º–µ–Ω—å—à–µ 300
        s/1111111111/D/g; s/DDDDDDDDDD/H/g; s/\(.\)\1*/&:/g; s/[ :]*$//; y/HD/11/

        s/^/Bin:/; G; s/\n/ /

        b @
    }
}

b @