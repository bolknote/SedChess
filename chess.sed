#n
# sed chess by Evgeny Stepanischev (http://bolknote.ru) Aug 2013
1s/.*/\
    @\
    figures()\
    board()\
    label(loop)\
        label(blackkingmove)\
            input()\
            move-white()\
            check-black-king-exists()\
            select-figures(K)\
            make-fake-kings()\
            select-figures(p)\
            label(pawnwhite)\
                iter-pawn()\
                break-if-end(pawnwhite)\
                delete-last-board()\
                store-only-iter()\
            back(pawnwhite)\
            select-figures(q)\
            label(queenwhite)\
                iter-queen()\
                break-if-end(queenwhite)\
                delete-last-board()\
                store-only-iter()\
            back(queenwhite)\
            select-figures(k)\
            label(kingwhitw)\
                iter-king()\
                break-if-end(kingwhite)\
                delete-last-board()\
                store-only-iter()\
            back(kingwhite)\
            select-figures(i)\
            label(bishopwhite)\
                iter-bishop()\
                break-if-end(bishopwhite)\
                delete-last-board()\
                store-only-iter()\
            back(bishopwhite)\
            select-figures(n)\
            label(knightwhite)\
                iter-knight()\
                break-if-end(knightwhite)\
                delete-last-board()\
                store-only-iter()\
            back(knightwhite)\
            select-figures(r)\
            label(rookwhite)\
                iter-rook()\
                break-if-end(rookwhite)\
                delete-last-board()\
                store-only-iter()\
            back(rookwhite)\
            check-mate()\
            break-if-end(blackkingmove)\
            move-black()\
            check-white-king-exists()\
            board()\
        back(blackkingmove)\
        board()\
        select-figures(K)\
        label(king)\
            iter-king()\
            break-if-end(king)\
            set-array()\
            estimate-white-pieces()\
            set-array()\
            estimate-black-pieces()\
            estimate-black-king()\
            sum-array()\
            sub-array()\
            delete-last-board()\
            store-iter()\
        back(king)\
        select-figures(P)\
        label(pawn)\
            iter-pawn()\
            break-if-end(pawn)\
            set-array()\
            estimate-white-pieces()\
            set-array()\
            estimate-black-pieces()\
            estimate-black-pawn()\
            sum-array()\
            sub-array()\
            delete-last-board()\
            store-iter()\
        back(pawn)\
        select-figures(Q)\
        label(queen)\
            iter-queen()\
            break-if-end(queen)\
            set-array()\
            estimate-white-pieces()\
            set-array()\
            estimate-black-pieces()\
            estimate-black-queen()\
            sum-array()\
            sub-array()\
            delete-last-board()\
            store-iter()\
        back(queen)\
        select-figures(I)\
        label(bishop)\
            iter-bishop()\
            break-if-end(bishop)\
            set-array()\
            estimate-white-pieces()\
            set-array()\
            estimate-black-pieces()\
            estimate-black-bishop()\
            sum-array()\
            sub-array()\
            delete-last-board()\
            store-iter()\
        back(bishop)\
        select-figures(N)\
        label(knight)\
            iter-knight()\
            break-if-end(knight)\
            set-array()\
            estimate-white-pieces()\
            set-array()\
            estimate-black-pieces()\
            estimate-black-knight()\
            sum-array()\
            sub-array()\
            delete-last-board()\
            store-iter()\
        back(knight)\
        select-figures(R)\
        label(rook)\
            iter-rook()\
            break-if-end(rook)\
            set-array()\
            estimate-white-pieces()\
            estimate-black-pieces()\
            sub-array()\
            delete-last-board()\
            store-iter()\
        back(rook)\
        find-best-move()\
        move-black()\
        board()\
        check-white-king-exists()\
        select-figures(k)\
        make-fake-kings()\
        select-figures(P)\
        label(pawnmoves)\
            iter-pawn()\
            break-if-end(pawnmoves)\
            delete-last-board()\
            store-only-iter()\
        back(pawnmoves)\
        select-figures(Q)\
        label(queenmoves)\
            iter-queen()\
            break-if-end(queenmoves)\
            delete-last-board()\
            store-only-iter()\
        back(queenmoves)\
        select-figures(K)\
        label(kingmoves)\
            iter-king()\
            break-if-end(kingmoves)\
            delete-last-board()\
            store-only-iter()\
        back(kingmoves)\
        select-figures(I)\
        label(bishopmoves)\
            iter-bishop()\
            break-if-end(bishopmoves)\
            delete-last-board()\
            store-only-iter()\
        back(bishopmoves)\
        select-figures(N)\
        label(knightmoves)\
            iter-knight()\
            break-if-end(knightmoves)\
            delete-last-board()\
            store-only-iter()\
        back(knightmoves)\
        select-figures(R)\
        label(rookmoves)\
            iter-rook()\
            break-if-end(rookmoves)\
            delete-last-board()\
            store-only-iter()\
        back(rookmoves)\
        check-mate()\
    back(loop)\
/


# –æ—Ü–µ–Ω–∫–∏ –∑–∞–ø—Ä–æ–≥—Ä–∞–º–º–∏—Ä–æ–≤–∞–Ω—ã –ø–æ –º–∞—Ç—Ä–∏—Ü–∞–º –∏–∑ –∫–Ω–∏–≥–∏
#¬†¬´–ü—Ä–æ–≥—Ä–∞–º–º–∏—Ä–æ–≤–∞–Ω–∏–µ —à–∞—Ö–º–∞—Ç –∏ –¥—Ä—É–≥–∏—Ö –ª–æ–≥–∏—á–µ—Å–∫–∏—Ö –∏–≥—Ä¬ª –ö–æ—Ä–Ω–∏–ª–æ–≤–∞ –ï–≤–≥–µ–Ω–∏—è –ù–∏–∫–æ–ª–∞–µ–≤–∏—á–∞

# –ø–µ—Ä–µ—Ñ–æ—Ä–º–∞—Ç–∏—Ä–æ–≤–∞–Ω–∏–µ –∫–æ–º–∞–Ω–¥
1s/ *//g; 1s/\n\n*/ /g; 1s/^ //

# –æ–±—Ä–∞–±–æ—Ç–∫–∞ –ø–æ—Å—Ç—É–ø–∞—é—â–µ–π –∫–æ–º–∞–Ω–¥—ã
1!{
    /^\([a-h][1-8]\) *\([a-h][1-8]\)$/ {
        s//\1 \2/
        # –¥–æ–±–∞–≤–ª—è–µ–º –ø–æ–ª—É—á–µ–Ω–Ω—ã–µ –∑–Ω–∞—á–µ–Ω–∏—è –≤–ø–µ—Ä–µ–¥–∏ —Å—Ç–µ–∫–∞ –∏—Å–ø–æ–ª–Ω–µ–Ω–∏—è
        G; s/\n/ /
        # –ø–µ—Ä–µ—Ö–æ–¥–∏–º –Ω–∞ –∏—Å–ø–æ–ª–Ω–µ–Ω–∏–µ –∫–æ–º–∞–Ω–¥
        b @
    }

    # –∏–≥—Ä–æ–∫ —Ö–æ—á–µ—Ç –≤—ã–π—Ç–∏
    /^q/ q

    # –≤–≤–µ–¥–µ–Ω–∞ –∫–∞–∫–∞—è-—Ç–æ –µ—Ä—É–Ω–¥–∞, —Å—Ç–∏—Ä–∞–µ–º –∏ –≤–æ–∑–≤—Ä–∞—â–∞–µ–º —Å—Ç–µ–∫ –∫–æ–º–∞–Ω–¥
    i\
    [12H[J[1A
    s/.*//

    g
    b
}

:@
s/@\([^ ]* \)/\1@/

# –Ω–∞—á–∞—Ç—å –º–∞—Å—Å–∏–≤
/@set-array()/ {
    s/^/ARRAY /
    b @
}

# –º–µ—Ç–∫–∞
/@label(/ {
    b @
}

# –ø–µ—Ä–µ—Ö–æ–¥ –∫ –º–µ—Ç–∫–µ
/@back(/ {
    s/label(\([^)]*\))\(.*\)@back(\1)/@label(\1)\2back(\1)/
    b @
}

# –≤—ã—Ö–æ–¥ –∏–∑ —Ü–∏–∫–ª–∞, –µ—Å–ª–∏ –Ω–∞ –≤–µ—Ä—à–∏–Ω–µ END
/@break-if-end(/ {
    /^END */{
        s///
        s/@break-if-end(\([^)]*\))\(.*\)back(\1)/break-if-end(\1)\2@back(\1)/
    }
    b @
}

# –≤–≤–æ–¥ –¥–∞–Ω–Ω—ã—Ö
/@input()/ {
    h; b
}

# —É–¥–∞–ª–µ–Ω–∏–µ –ø–æ—Å–ª–µ–¥–Ω–µ–π –∫–æ–ø–∏–∏ –¥–æ—Å–∫–∏
/@delete-last-board()/ {
    s/\(.*\)Board:[^ ]* */\1/
    b @
}

# –¥—É–±–ª–∏—Ä–æ–≤–∞–Ω–∏–µ –¥–æ—Å–∫–∏
/@copy-board()/ {
    s/\(Board:[^ ]*\)/\1 \1/
    b @
}

# –≥–µ–Ω–µ—Ä–∞—Ü–∏—è –Ω–∞—á–∞–ª—å–Ω–æ–≥–æ —Å–æ—Å—Ç–æ—è–Ω–∏—è –¥–æ—Å–∫–∏
/@figures()/ {
    # —Ñ–æ—Ä–º–∞—Ç: XYFig
    # –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã –±–µ–ª—ã—Ö —Ç—É—Ç –∏ –¥–∞–ª—å—à–µ –¥–æ–ª–∂–Ω—ã –∏–¥—Ç–∏ –ù–ò–ñ–ï —á—ë—Ä–Ω—ã—Ö
    # –ë–û–õ–¨–®–ò–ï ‚Äî —á—ë—Ä–Ω—ã–µ, –º–∞–ª–µ–Ω—å–∫–∏–µ¬†‚Äî –±–µ–ª—ã–µ
    s/^/Board:\
a8Rb8Nc8Id8Qe8Kf8Ig8Nh8R\
a7Pb7Pc7Pd7Pe7Pf7Pg7Ph7P\
a6¬†b6¬†c6¬†d6¬†e6¬†f6¬†g6¬†h6¬†\
a5¬†b5¬†c5¬†d5¬†e5¬†f5¬†g5¬†h5¬†\
a4¬†b4¬†c4¬†d4¬†e4¬†f4¬†g4¬†h4¬†\
a3¬†b3¬†c3¬†d3¬†e3¬†f3¬†g3¬†h3¬†\
a2pb2pc2pd2pe2pf2pg2ph2p\
a1rb1nc1id1qe1kf1ig1nh1r /
# –ø—Ä–æ–±–µ–ª –≤ –∫–æ–Ω—Ü–µ –Ω—É–∂–µ–Ω!

#     s/^/Board:\
# a8¬†b8¬†c8¬†d8¬†e8Kf8¬†g8¬†h8r\
# a7¬†b7¬†c7¬†d7¬†e7¬†f7¬†g7¬†h7¬†\
# a6¬†b6¬†c6¬†d6¬†e6nf6¬†g6¬†h6¬†\
# a5¬†b5¬†c5¬†d5¬†e5¬†f5¬†g5¬†h5¬†\
# a4¬†b4¬†c4¬†d4¬†e4¬†f4¬†g4¬†h4¬†\
# a3¬†b3¬†c3¬†d3¬†e3¬†f3¬†g3¬†h3¬†\
# a2¬†b2¬†c2¬†d2¬†e2¬†f2¬†g2¬†h2k\
# a1¬†b1¬†c1¬†d1¬†e1¬†f1¬†g1¬†h1¬† /

    s/\n//g

    b @
}

# –≤—ã–≤–æ–¥ –¥–æ—Å–∫–∏
/@board()/ {
    # —Å–æ—Ö—Ä–∞–Ω—è–µ–º —Å—Ç–µ–∫ –∫–æ–º–∞–Ω–¥
    h
    # —É–±–∏—Ä–∞–µ–º –≤—Å—ë, –∫—Ä–æ–º–µ –¥–æ—Å–∫–∏ (–±–µ—Ä—ë–º –≤—Å–µ–≥–¥–∞ –ø–æ—Å–ª–µ–¥–Ω—é—é –¥–æ—Å–∫—É)
    s/.*Board://
    s/ .*$//
    # —Ä–∞—Å—à–∏—Ñ—Ä–æ–≤—ã–≤–∞–µ–º –¥–æ—Å–∫—É
    # Pawn, Queen, King, bIshop, kNight, Rook, Zero¬†‚Äî –ø—Å–µ–≤–¥–æ–∫–æ—Ä–æ–ª—å –¥–ª—è –æ–ø—Ä–µ–¥–ª–µ–Ω–∏—è –º–∞—Ç–∞ –∏ —à–∞—Ö–∞
    s/..z//g
    y/pqkinrPQKINR12345678abcd/‚ôü‚ôõ‚ôö‚ôù‚ôû‚ôú‚ôô‚ôï‚ôî‚ôó‚ôò‚ôñ987654323579/
    s/\([1-9e-h]\)\([1-9]\)\(.\)/[\2;\1H\3 /g

    # —Ä–∞—Å—Ü–≤–µ—á–∏–≤–∞–µ–º
    s/[8642];[37eg]H/&[48;5;209;37;1m/g
    s/[9753];[37eg]H/&[48;5;94;37;1m/g
    s/[8642];[59fh]H/&[48;5;94;37;1m/g
    s/[9753];[59fh]H/&[48;5;209;37;1m/g

    # –¥–≤—É–∑–Ω–∞—á–Ω—ã–µ —á–∏—Å–ª–∞
    s/e/11/g;s/f/13/g;s/g/15/g;s/h/17/g

    s/$/[0m[11H/
    # –≤—ã–≤–æ–¥–∏–º –¥–æ—Å–∫—É –∏ –≤–æ–∑–≤—Ä–∞—â–∞–µ–º –≤—Å—ë –∫–∞–∫ –±—ã–ª–æ
    i\
[2J[1;3Ha b c d e f g h\
8\
7\
6\
5\
4\
3\
2\
1\
\
–í–≤–µ–¥–∏—Ç–µ –∫–æ–º–∞–Ω–¥—É
    p
    g

    b @
}

# –¥–µ–ª–∞–µ–º —Ö–æ–¥ –ø–æ –≤–≤–µ–¥—ë–Ω–Ω—ã–º –ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª–µ–º –¥–∞–Ω–Ω—ã–º
/@move-white()/ {
    # –≥–∞—Ä–¥—ã –æ—Å–Ω–æ–≤–Ω—ã—Ö —Ä–µ–≥—É–ª—è—Ä–æ–∫ (–∏—Ö –Ω—É–∂–Ω–æ —Ç—â–∞—Ç–µ–ª—å–Ω–æ –∑–∞—â–∏—â–∞—Ç—å –æ—Ç –Ω–µ—Å—Ä–∞–±–∞—Ç—ã–≤–∞–Ω–∏–π,
    # –∏–Ω–∞—á–µ sed –≤—ã–¥–∞—Å—Ç –æ—à–∏–±–∫—É –∏ –æ—Å—Ç–∞–Ω–æ–≤–∏—Ç—Å—è)
    # –≤—ã—á–∏—â–∞–µ–º –≤—Å—ë, –∫—Ä–æ–º–µ –¥–æ—Å–∫–∏ –∏ –ø–µ—Ä–≤—ã—Ö –¥–≤—É—Ö –∑–Ω–∞—á–µ–Ω–∏–π
    h; s/\([^ ]*\) \([^ ]*\).*Board:\([^ ]*\).*/\1 \2 \3/
    
    # –≤—ã–¥–µ–ª—è–µ–º —É–∫–∞–∑–∞–Ω–Ω—ã–µ –∫–ª–µ—Ç–∫–∏
    s/\([^ ]*\) [^ ]* .*\(\1.\)/&(1:\2)/
    s/[^ ]* \([^ ]*\) .*\(\1.\)/&(2:\2)/
    # —Ç–µ–ø–µ—Ä—å –æ–Ω–∏ –∏–º–µ—é—Ç —Ñ–æ—Ä–º–∞—Ç:
    # –Ω–æ–º–µ—Ä_–ø–æ_–ø–æ—Ä—è–¥–∫—É_–≤–≤–æ–¥–∞:XY–§–∏–≥—É—Ä–∞
    s/.*(\(.....\)).*(\(.....\)).*/\1 \2/

    # —Ç–µ–ø–µ—Ä—å –Ω–∞–¥–æ –ø—Ä–æ–≤–µ—Ä–∏—Ç—å:
    # 1. —á—Ç–æ –±–µ—Ä—ë–º –Ω–µ —á—É–∂—É—é –∏ –Ω–µ –ø—É—Å—Ç—É—é —Ñ–∏–≥—É—Ä—É
    /1:..[PQKINR¬†]/ {
        g; s/[^ ]* [^ ]* *//; b @
    }

    # 2. –Ω–µ –∫–ª–∞–¥—ë–º –Ω–∞ –º–µ—Å—Ç–æ —Å–≤–æ–µ–π —Ñ–∏–≥—É—Ä—ã
    /2:..[pqkinr]/ {
        g; s/[^ ]* [^ ]* *//; b @
    }

    # –ø–æ—Ä—è–¥–æ–∫ —Ç–∞–∫–æ–π:
    # —É–∫–∞–∑–∞–Ω–Ω—ã–µ –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã —É –Ω–∞–π–¥–µ–Ω–Ω—ã—Ö —Ñ–∏–≥—É—Ä –º–µ–Ω—è–µ–º –º–µ–∂–¥—É —Å–æ–±–æ–π

    # –µ—Å–ª–∏ —Ö–æ–¥ –±—É–¥–µ—Ç –≤–ø–µ—Ä—ë–¥‚Ä¶
    /2:.*1:/ {
        g
        #    1        2                3            4           5
        /\([^ ]*\) \([^ ]*\) \(.*Board:[^ ]*\)\2.\([^ ]*\)\1\([pqkinr]\)/ {
            s//\3\1¬†\4\2\5/
            b move-white::checkpawn
        }
    }

    # —Ö–æ–¥ –Ω–∞–∑–∞–¥
    g
    #     1         2            3                4          5
    s/\([^ ]*\) \([^ ]*\) \(.*Board:[^ ]*\)\1\([pqkinr]\)\([^ ]*\)\2./\3\2\4\5\1¬†/

    # –ø—Ä–æ–≤–µ—Ä—è–µ–º, –Ω–µ –æ–∫–∞–∑–∞–ª–∞—Å—å –ª–∏ –ø–µ—à–∫–∞ –Ω–∞ 8-–π –≥–æ—Ä–∏–∑–æ–Ω—Ç–∞–ª–∏, –µ—Å–ª–∏ –¥–∞, —Ç–æ –Ω–∞–¥–æ –ø—Ä–µ–æ–±—Ä–∞–∑–æ–≤–∞—Ç—å
    # –µ—ë –≤ —Ñ–µ—Ä–∑—è (–∑–∞ —Ö–æ–¥ –º–æ–∂–µ—Ç –±—ã—Ç—å —Ç–æ–ª—å–∫–æ –æ–¥–Ω–∞ —Ç–∞–∫–∞—è –ø–µ—à–∫–∞)
    :move-white::checkpawn
    s/\([a-h]8\)p/\1q/

    b @
}

# –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ –æ—Å—Ç–∞–≤—à–∏—Ö—Å—è —Ñ–∏–≥—É—Ä
/@count-pieces()/ {
    h
    # —É–±–∏—Ä–∞–µ–º –≤—Å—ë, –∫—Ä–æ–º–µ –¥–æ—Å–∫–∏
    s/.*Board://
    s/ .*$//
    # —É–±–∏—Ä–∞–µ–º –≤—Å—ë, –∫—Ä–æ–º–µ –±–µ–ª—ã—Ö —Ñ–∏–≥—É—Ä
    s/[^pqkinrPQKINR]//g
    # —Å—á–∏—Ç–∞–µ–º
    s/./1/g
    # –≤–æ–∑–≤—Ä–∞—â–∞–µ–º —Å—Ç–µ–∫ –∫–æ–º–∞–Ω–¥
    G
    # –ø–æ—Å–ª–µ G –ø–æ—è–≤–∏–ª—Å—è –ø–µ—Ä–µ–≤–æ–¥ —Å—Ç—Ä–æ–∫–∏, —É–±–∏—Ä–∞–µ–º –µ–≥–æ
    s/\n/ /

    b @
}

#–æ—Ü–µ–Ω–æ—á–Ω–∞—è —Ñ—É–Ω–∫—Ü–∏—è –∏–º–µ—é—â–∏—Ö—Å—è —á—ë—Ä–Ω—ã—Ö —Ñ–∏–≥—É—Ä
/@estimate-black-pieces()/ {
    # –ø–µ—à–∫–∞¬†‚Äî 100, —Å–ª–æ–Ω –∏ –∫–æ–Ω—å¬†‚Äî 300, –ª–∞–¥—å—è¬†‚Äî 500, —Ñ–µ—Ä–∑—å¬†‚Äî 900, –∫–æ—Ä–æ–ª—å¬†‚Äî 9000,
    # –µ—â—ë 9000¬†‚Äî –ø—Å–µ–≤–¥–æ—Ñ–∏–≥—É—Ä–∞ —á—ë—Ä–Ω—ã—Ö, —á—Ç–æ–±—ã –Ω–µ –±—ã–ª–æ –ø–µ—Ä–µ–ø–æ–ª–Ω–µ–Ω–∏—è = 21900

    # –æ—á–∏—Å—Ç–∫–∞ –≤—Å–µ–≥–æ –ª–∏—à–Ω–µ–≥–æ
    h; s/.*Board://; s/ .*$//

    # —É–±–∏—Ä–∞–µ–º –≤—Å—ë, –∫—Ä–æ–º–µ –ø–æ–¥—Å—á–∏—Ç—ã–≤–∞–µ–º—ã—Ö —Ñ–∏–≥—É—Ä
    s/[^PINRQK]//g

    # —Å—á–∏—Ç–∞–µ–º –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ * –∫–æ—ç—Ñ—Ñ–∏—Ü–∏–µ–Ω—Ç —Ñ–∏–≥—É—Ä—ã (—Ñ–µ—Ä–∑—å¬†Q ‚Äî –µ–¥–∏–Ω—Å—Ç–≤–µ–Ω–Ω—ã–π, –Ω–æ –º–æ–∂–µ—Ç –ø–æ—è–≤–∏—Ç—å—Å—è –µ—â—ë —á–µ—Ä–µ–∑ –ø–µ—à–∫—É)
    s/P/1/g; s/[NI]/111/g; s/R/11111/g; s/Q/111111111/g
    # –∫–æ—Ä–æ–ª—å, —Å—Ç–∞–≤–∏–º –≤–ø–µ—Ä—ë–¥ –∫ –ø—Å–µ–≤–¥–æ—Ñ–∏–≥—É—Ä–µ
    s/^\(.*\)K/HHHHHHHHH\1/
    # —á—ë—Ä–Ω–∞—è –ø—Å–µ–≤–¥–æ—Ñ–∏–≥—É—Ä–∞
    s/^/HHHHHHHHH/

    # –≥—Ä—É–ø–ø–∏—Ä—É–µ–º —Å–æ—Ç–Ω–∏ –∏ —Ç—ã—Å—è—á–∏
    s/1111111111/H/g; s/HHHHHHHHHH/T/g

    # –≤—Å—Ç–∞–≤–ª—è–µ–º –¥–≤–æ–µ—Ç–æ—á–∏—è
    s/\(.\)\1*/&:/g
    # –µ—Å–ª–∏ –Ω–µ—Ç –µ–¥–∏–Ω–∏—Ü, –≤ –∫–æ–Ω–µ—Ü¬†‚Äî –µ—â—ë –æ–¥–Ω–æ –¥–≤–æ–µ—Ç–æ—á–∏–µ
    /1/ ! s/$/:/
    # –µ—Å–ª–∏ –Ω–µ—Ç —Å–æ—Ç–µ–Ω, —Ç–æ –¥–æ –µ–¥–∏–Ω–∏—Ü –∏–ª–∏ –ø–æ—Å–ª–µ–¥–Ω–µ–≥–æ –¥–≤–æ–µ—Ç–æ—á–∏—è¬†‚Äî –µ—â—ë –¥–≤–æ–µ—Ç–æ—á–∏–µ
    /H/ ! s/[1:]/:&/

    y/HT/11/; s/$/:B/
    # –¥–æ–±–∞–≤–ª—è–µ–º –∫ —Å–æ—Ö—Ä–∞–Ω—ë–Ω–Ω–æ–º—É —Å—Ç–µ–∫—É
    G; s/\n/ /

    b @
}

#–æ—Ü–µ–Ω–æ—á–Ω–∞—è —Ñ—É–Ω–∫—Ü–∏—è –∏–º–µ—é—â–∏—Ö—Å—è –±–µ–ª—ã—Ö —Ñ–∏–≥—É—Ä
/@estimate-white-pieces()/ {
    # –ø–µ—à–∫–∞¬†‚Äî 100, —Å–ª–æ–Ω –∏ –∫–æ–Ω—å¬†‚Äî 300, –ª–∞–¥—å—è¬†‚Äî 500, —Ñ–µ—Ä–∑—å¬†‚Äî 900, –∫–æ—Ä–æ–ª—å¬†‚Äî 9000

    # –æ—á–∏—Å—Ç–∫–∞ –≤—Å–µ–≥–æ –ª–∏—à–Ω–µ–≥–æ
    h; s/.*Board://; s/ .*$//
    # —É–±–∏—Ä–∞–µ–º –≤—Å—ë, –∫—Ä–æ–º–µ –ø–æ–¥—Å—á–∏—Ç—ã–≤–∞–µ–º—ã—Ö —Ñ–∏–≥—É—Ä
    s/[^pinrqk]//g
    # —Å—á–∏—Ç–∞–µ–º –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ * –∫–æ—ç—Ñ—Ñ–∏—Ü–∏–µ–Ω—Ç —Ñ–∏–≥—É—Ä—ã (—Ñ–µ—Ä–∑—å¬†q ‚Äî –µ–¥–∏–Ω—Å—Ç–≤–µ–Ω–Ω—ã–π, –Ω–æ –º–æ–∂–µ—Ç –ø–æ—è–≤–∏—Ç—å—Å—è –µ—â—ë —á–µ—Ä–µ–∑ –ø–µ—à–∫—É)
    s/p/1/g; s/[ni]/111/g; s/r/11111/g; s/q/111111111/g

    # –∫–æ—Ä–æ–ª—å, —Å—Ç–∞–≤–∏–º –≤–ø–µ—Ä—ë–¥
    s/^\(.*\)k/HHHHHHHHH\1/

    # –≥—Ä—É–ø–ø–∏—Ä—É–µ–º —Å–æ—Ç–Ω–∏ –∏ —Ç—ã—Å—è—á–∏
    s/1111111111/H/g; s/HHHHHHHHHH/T/g

    # –≤—Å—Ç–∞–≤–ª—è–µ–º –¥–≤–æ–µ—Ç–æ—á–∏—è
    s/\(.\)\1*/&:/g
    # –µ—Å–ª–∏ –Ω–µ—Ç –µ–¥–∏–Ω–∏—Ü, –≤ –∫–æ–Ω–µ—Ü¬†‚Äî –µ—â—ë –æ–¥–Ω–æ –¥–≤–æ–µ—Ç–æ—á–∏–µ
    /1/ ! s/$/:/
    # –µ—Å–ª–∏ –Ω–µ—Ç —Å–æ—Ç–µ–Ω, —Ç–æ –¥–æ –µ–¥–∏–Ω–∏—Ü –∏–ª–∏ –ø–æ—Å–ª–µ–¥–Ω–µ–≥–æ –¥–≤–æ–µ—Ç–æ—á–∏—è¬†‚Äî –µ—â—ë –¥–≤–æ–µ—Ç–æ—á–∏–µ
    /H/ ! s/[1:]/:&/

    y/HT/11/; s/$/:B/
    # –¥–æ–±–∞–≤–ª—è–µ–º –∫ —Å–æ—Ö—Ä–∞–Ω—ë–Ω–Ω–æ–º—É —Å—Ç–µ–∫—É
    G; s/\n/ /

    b @
}

#–¥–ª—è –æ—Ç–ª–∞–¥–∫–∏: –≤—ã–≤–æ–¥ —Ç–µ–∫—É—â–µ–≥–æ —Å—Ç–µ–∫–∞
/@log()/ {
    l
    q
}

/@l()/ {
    h
    l
    w chess.log
    g
}

#–æ—Ü–µ–Ω–æ—á–Ω–∞—è —Ñ—É–Ω–∫—Ü–∏—è –¥–ª—è –ø–æ–∑–∏—Ü–∏–∏ —á—ë—Ä–Ω—ã—Ö –ø–µ—à–µ–∫
/@estimate-black-pawn()/ {
    # –æ—á–∏—Å—Ç–∫–∞ –≤—Å–µ–≥–æ –ª–∏—à–Ω–µ–≥–æ
    h; s/.*Board://; s/ .*$//
    # –æ—Å—Ç–∞–≤–ª—è–µ–º —Ç–æ–ª—å–∫–æ —á—ë—Ä–Ω—ã–µ –∏ –±–µ–ª—ã–µ –ø–µ—à–∫–∏, –ø–µ—Ä–µ–∫–æ–¥–∏—Ä—É–µ–º –∏—Ö –≤ –ø–æ–Ω—è—Ç–Ω—ã–µ –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã
    # —Ç–µ–ø–µ—Ä—å –ø–µ—à–∫–∏ –∑–∞–ø–∏—Å–∞–Ω—ã –≤–æ—Ç —Ç–∞–∫: X–¶–≤–µ—Ç (–≥–¥–µ –¶–≤–µ—Ç ‚Äî Black –∏–ª–∏ White), —Ä–∞–∑–¥–µ–ª–µ–Ω—ã –ø—Ä–æ–±–µ–ª–æ–º
    s/[a-h][1-8][^Pp]//g; y/Ppabcdefgh/BW12345678/; s/\([1-8]\)[1-8]/ \1/g

    # ‚Üí –≠—Ç–∞–ø 1
    # –∏—â–µ–º —á—ë—Ä–Ω—ã–µ –ø–µ—à–∫–∏, –Ω–∞ –≤–µ—Ä—Ç–∏–∫–∞–ª–∏ —É –∫–æ—Ç–æ—Ä—ã—Ö —Å—Ç–æ—è—Ç –±–µ–ª—ã–µ, –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã –±–µ–ª—ã—Ö –∏–¥—É—Ç
    # –≤—Å–µ–≥–¥–∞ –ü–û–°–õ–ï –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç —á—ë—Ä–Ω—ã—Ö
    :estimate-black-pawn::black
    /\([1-8]\)B\(.*\1\)W/ {
        s//\1b\2W/
        b estimate-black-pawn::black
    }

    # ‚Üí –≠—Ç–∞–ø 2.1
    # –ø–µ—Ä–µ–≤–æ–¥–∏–º –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã –≤ –ø–æ—Å–ª–µ–¥–æ–≤–∞—Ç–µ–ª—å–Ω–æ—Å—Ç–∏ –¥–ª–∏–Ω—ã X
    :estimate-black-pawn::x
    /[2-8]/ {
        s/[2-8]/1&/g
        y/2345678/1234567/

        b estimate-black-pawn::x
    }

    # ‚Üí –≠—Ç–∞–ø 2.2
    # –∏—â–µ–º –ø–µ—à–∫–∏, –Ω–µ –æ—Ç—Å–µ—è–Ω–Ω—ã–µ –Ω–∞ —ç—Ç–∞–ø–µ 1, —É –∫–æ—Ç–æ—Ä—ã—Ö –Ω–∞ —Å–æ—Å–µ–¥–Ω–µ–π –ª–∏–Ω–∏–∏ —Å–ª–µ–≤–∞ —Å—Ç–æ—è—Ç –±–µ–ª—ã–µ
    :estimate-black-pawn::left
    /\( 1*\)B\(.*\11\)W/ {
        s//\1b\2W/
        b estimate-black-pawn::left
    }

    # ‚Üí –≠—Ç–∞–ø 2.3
    # –∏—â–µ–º –ø–µ—à–∫–∏, –Ω–µ –æ—Ç—Å–µ—è–Ω–Ω—ã–µ –Ω–∞ —ç—Ç–∞–ø–µ 2, —É –∫–æ—Ç–æ—Ä—ã—Ö –Ω–∞ —Å–æ—Å–µ–¥–Ω–µ–π –ª–∏–Ω–∏–∏ —Å–ø—Ä–∞–≤–∞ —Å—Ç–æ—è—Ç –±–µ–ª—ã–µ
    :estimate-black-pawn::right
    / 1\(1*\)B\(.* \1\)W/ {
        s// 1\1b\2W/
        b estimate-black-pawn::right
    }

    # –í –∏—Ç–æ–≥–µ, W¬†‚Äî –±–µ–ª—ã–µ –ø–µ—à–∫–∏, b¬†‚Äî —á—ë—Ä–Ω—ã–µ, B¬†‚Äî —á—ë—Ä–Ω—ã–µ —Å–≤–æ–±–æ–¥–Ω—ã–µ –ø–µ—à–∫–∏
    # –∏–∑–±–∞–≤–ª—è–µ–º—Å—è –æ—Ç –Ω–µ—Å–≤–æ–±–æ–¥–Ω—ã—Ö –∏ –±–µ–ª—ã—Ö –ø–µ—à–µ–∫
    s/ [^ ]*[Wb]//g

    # ‚Üí –≠—Ç–∞–ø 3
    # —Å—á–∏—Ç–∞–µ–º —Å—Ç–æ–∏–º–æ—Å—Ç–∏ —á—ë—Ä–Ω—ã—Ö —Å–≤–æ–±–æ–¥–Ω—ã—Ö –ø–µ—à–µ–∫
    s/ 1B//; s/ 11B/ ::11111B/; s/ 111B/ :1:B/; s/ 1111B/ :1:11111B/; s/ 11111B/ :11:B/
    s/ 111111B/ :111:B/; s/ 1111111B/ 1:1111:B/; s/ 11111111B//

    # ‚Üí –≠—Ç–∞–ø 4
    # —Å–æ—Ö—Ä–∞–Ω—è–µ–º –ø–æ–ª—É—á–µ–Ω–Ω–æ–µ, –≥—Ä—É–∑–∏–º —Å—Ç–µ–∫ –æ–±—Ä–∞—Ç–Ω–æ, –≤—ã—Ä–µ–∑–∞–µ–º –¥–æ—Å–∫—É –∏ –æ—Å—Ç–∞–≤–ª—è–µ–º —á—ë—Ä–Ω—ã–µ –ø–µ—à–∫–∏ —Å –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç–∞–º–∏
    G; h; s/.*Board://; s/ .*$//; s/[a-h][1-8][^p]//g

    # –æ—Ü–µ–Ω–∏–≤–∞–µ–º –ø–æ–∑–∏—Ü–∏–∏ –≤—Å–µ—Ö –ø–µ—à–µ–∫
    s/.[81]p/::B/g

    s/[abcfgh]7p/::1111B/g; s/[de]7p/::B/g

    s/[ah][65]p/::111111B/g; s/[bg][65]p/::11111111B/g; s/[cf]6p/::11B/g; s/[de]6p/:1:B/g

    s/[bg]5p/:1:11B/g; s/[cf]5p/:1:111111B/g; s/[de]5p/:11:1111B/g

    s/[ah]4p/::11111111B/g; s/[bg]4p/:1:11B/g; s/[cf]4p/:1:111111B/g; s/[de]4p/:11:1111B/g

    s/[ah][32]p/:1:11B/g; s/[bg][32]p/:1:111111B/g; s/[cf][32]p/:11:1111B/g; s/[de][32]p/:111:11B/g

    # –≤—Å—Ç–∞–≤–ª—è–µ–º –ø—Ä–æ–±–µ–ª—ã –º–µ–∂–¥—É –æ—Ü–µ–Ω–∫–∞–º–∏
    s/B/& /g; s/^/ /

    # ‚Üí –≠—Ç–∞–ø 5
    # –≤–æ–∑–≤—Ä–∞—â–∞–µ–º —Å–æ—Ö—Ä–∞–Ω—ë–Ω–Ω—ã–µ –æ—Ü–µ–Ω–∫–∏, —É–±–∏—Ä–∞–µ–º –æ—Å—Ç–∞—Ç–∫–∏ —Å—Ç–µ–∫–∞
    G; s/\n\(.*\)\n.*/ \1/

    # –¥–æ–±–∞–≤–ª—è–µ–º –∫ —Å–æ—Ö—Ä–∞–Ω—ë–Ω–Ω–æ–º—É —Å—Ç–µ–∫—É, –≤—ã—á–∏—â–∞–µ–º –Ω–∞—à –º—É—Å–æ—Ä, –∫–æ—Ç–æ—Ä—ã–π –º—ã —Å–∫–ª–∞–¥—ã–≤–∞–ª–∏ –≤—ã—à–µ¬†‚Äî
    # —Ç–∞–º –≤—Ç–æ—Ä–æ–π —Å—Ç—Ä–æ–∫–æ–π –ª–µ–∂–∞—Ç –æ—Ü–µ–Ω–∫–∏
    G; s/\n.*\n/ /

    b @
}

#–æ—Ü–µ–Ω–æ—á–Ω–∞—è —Ñ—É–Ω–∫—Ü–∏—è –¥–ª—è –ø–æ–∑–∏—Ü–∏–∏ —á—ë—Ä–Ω–æ–≥–æ –∫–æ—Ä–æ–ª—è
/@estimate-black-king()/ {
    h; s/.*Board://; s/ .*$//

    # –≤—ã–¥–µ–ª—è–µ–º –∫–æ—Ä–æ–ª—è
    s/[a-h][1-8][^K]//g

    # —Å—á–∏—Ç–∞–µ–º –µ–≥–æ –≤–µ—Å (–º–∞—Ç—Ä–∏—Ü–∞ –∫–æ–Ω—Ü–∞ –∏–≥—Ä—ã)
    s/[ah][18]./::/
    
    s/[de][54]./:111:111111/
    
    s/[cf][54]./:111:/; s/[de][63]./:111:/

    s/[bg][54]./:11:1111/; s/[de][72]./:11:1111/; s/[cf][63]./:11:1111/

    s/[de][18]./:1:11111111/; s/[ah][54]./:1:11111111/; s/[cf][72]./:1:11111111/; s/[bg][63]./:1:11111111/

    s/[bg][72]./:1:11/; s/[ah][63]./:1:11/; s/[cf][81]./:1:11/

    s/[a-h][1-9]./::111111/

    G; s/\n/B /

    b @
}

#–æ—Ü–µ–Ω–æ—á–Ω–∞—è —Ñ—É–Ω–∫—Ü–∏—è –¥–ª—è –ø–æ–∑–∏—Ü–∏–∏ —á—ë—Ä–Ω–æ–≥–æ –∫–æ–Ω—è
/@estimate-black-knight()/ {
    h; s/.*Board://; s/ .*$//

    # –≤—ã–¥–µ–ª—è–µ–º –∫–æ–Ω–µ–π
    s/[a-h][1-8][^N]//g

    # —Å—á–∏—Ç–∞–µ–º –∏—Ö –≤–µ—Å
    s/[ah][18]./::B/g
    
    s/[de][54]./:111:11B/g
    
    s/[cf][54]./:11:11111111B/g; s/[de][63]./:11:11111111B/g

    s/[cf][36]./:11:1111B/g

    s/[bg][54]./:11:B/g; s/[de][72]./:11:B/g; s/[cf][63]./:11:B/g

    s/[de][18]./:1:B/g; s/[ah][54]./:1:B/g; s/[cf][72]./:1:B/g; s/[bg][63]./:1:B/g

    s/[bg][72]./::11111111B/g; s/[ah][63]./::11111111B/g; s/[cf][81]./::11111111B/g

    s/[a-h][1-9]./::1111B/g

    s/B/& /g; G; s/ *\n/ /

    b @
}

#–æ—Ü–µ–Ω–æ—á–Ω–∞—è —Ñ—É–Ω–∫—Ü–∏—è –¥–ª—è –ø–æ–∑–∏—Ü–∏–∏ —á—ë—Ä–Ω–æ–≥–æ —Å–ª–æ–Ω–∞
/@estimate-black-bishop()/ {
    h; s/.*Board://; s/ .*$//

    # –≤—ã–¥–µ–ª—è–µ–º —Å–ª–æ–Ω–æ–≤
    s/[a-h][1-8][^I]//g

    # —Å—á–∏—Ç–∞–µ–º –∏—Ö –≤–µ—Å
    s/[a-h][81]./:::1:1111B/g; s/[ah][1-8]./:::1:1111B/g

    s/[bg][72]./:::11:11B/g; s/[c-f][3-6]/:::11:11B/g

    s/[a-h][1-9]./:::1:11111111B/g

    s/B/& /; G; s/\n/ /

    b @
}

#–æ—Ü–µ–Ω–æ—á–Ω–∞—è —Ñ—É–Ω–∫—Ü–∏—è –¥–ª—è –ø–æ–∑–∏—Ü–∏–∏ —á—ë—Ä–Ω–æ–π –∫–æ—Ä–æ–ª–µ–≤—ã (—Ñ–µ—Ä–∑—è)
/@estimate-black-queen()/ {
    h; s/.*Board://; s/ .*$//

    # –≤—ã–¥–µ–ª—è–µ–º —Ñ–µ—Ä–∑—è –∏ –≤—Ä–∞–∂–µ—Å–∫–æ–≥–æ –∫–æ—Ä–æ–ª—è
    s/[a-h][1-8][^Qk]//g

    # –µ—Å–ª–∏ –æ–¥–Ω–æ–π –∏–∑ —Ñ–∏–≥—É—Ä –Ω–∞ –ø–æ–ª–µ –Ω–µ—Ç, –≤–æ–∑–≤—Ä–∞—Ç
    /Q/,/k/ ! {
        g; b @
    }

    # –∫–æ—Ä–æ–ª—è –≤—ã–¥–≤–∏–≥–∞–µ–º –≤–ø–µ—Ä—ë–¥
    s/\(.*\)\(..k\)\(.*\)/\2\1\3/

    # –µ—Å–ª–∏ —É –Ω–∞—Å –∏–º–µ–µ—Ç—Å—è –≤—Ç–æ—Ä–æ–π —Ñ–µ—Ä–∑—å, —Ç–æ —Å—Ç–∞–≤–∏–º –µ–≥–æ —á–µ—Ä–µ–∑ —Ä–∞–∑–¥–µ–ª–∏—Ç–µ–ª—å
    # –∏ –≤–ø–µ—Ä–µ–¥–∏ –∫ –Ω–µ–º—É –ª–µ–ø–∏–º –∫–æ—Ä–æ–ª—è
    s/\(..k\)\(..Q\)\(..Q\)/ \1\2\# \1\3/

    # —Ñ–∏–≥—É—Ä—ã —É–±–∏—Ä–∞–µ–º, –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã –∫ —á–∏—Å–ª–∞–º, —Ä–∞—Å—Å—Ç–∞–≤–ª—è–µ–º –ø—Ä–æ–±–µ–ª—ã –≤–æ–∫—Ä—É–≥ —á–∏—Å–µ–ª –¥–ª—è –æ–≥—Ä–∞–Ω–∏—á–µ–Ω–∏—è
    y/abcdefgh/12345678/; s/\([1-9]\)\(.\)./\1 \2 /g

    # –≥—Ä—É–ø–ø–∏—Ä—É–µ–º –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã, –ø–æ–ª—É—á–∏—Ç—Å—è X1 X2 Y1 Y2 (–Ω–∞ –¥–≤—É—Ö —Å—Ç—Ä–æ–∫–∞—Ö)
    # —É –Ω–∞—Å –∑–∞—Ö–≤–∞—Ç—ã–≤–∞–µ—Ç—Å—è –ø–æ —Ç—Ä–∏ –∑–Ω–∞—á–µ–Ω–∏—è, –Ω–æ –ø—É—Ç–∞–Ω–∏—Ü—ã –Ω–µ –±—É–¥–µ—Ç, —Ç.–∫. –∏—Å–ø–æ–ª—å–∑—É—é—Ç—Å—è –ø—Ä–æ–±–µ–ª—ã,
    # –∞ –º–µ–∂–¥—É –ø–∞—Ä–∞–º–∏ —á–∏—Å–µ–ª¬†‚Äî ¬´—Ä–µ—à—ë—Ç–∫–∞¬ª
    s/\([1-8]\) \([1-8]\) \([1-8]\)/\1 \3 \2/g

    # –ø–µ—Ä–µ–≤–æ–¥–∏–º –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã –≤ –ø–æ—Å–ª–µ–¥–æ–≤–∞—Ç–µ–ª—å–Ω–æ—Å—Ç–∏ –¥–ª–∏–Ω—ã –∑–Ω–∞—á–µ–Ω–∏–π –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç
    :estimate-black-queen::xy
    /[2-8]/ {
        s/[2-8]/1&/g
        y/2345678/1234567/

        b estimate-black-queen::xy
    }

    # –¥–æ–ª–∂–Ω–æ –ø–æ–ª—É—á–∏—Ç—å—Å—è (8¬†‚Äî –Ω–∞–∏–±–æ–ª—å—à–µ–µ —Ä–∞—Å—Ç–æ—è–Ω–∏–µ):
    # 8 - (XM-Xm) + 8 - (YM-Ym) => 16-XM+Xm-YM+Ym => 16-(XM+YM)+(Xm+Ym)

    # —Å–æ—Ä—Ç–∏—Ä–æ–≤–∫–∞¬†‚Äî –±–æÃÅ–ª—å—à–∞—è –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç–∞ –≤–ø–µ—Ä—ë–¥
    s/ \(11*\) \(\111*\)/ \2 \1/g

    # –ø–æ–ª—É—á–∞–µ—Ç—Å—è 4 —á–∏—Å–ª–∞ –¥–ª—è –∫–∞–∂–¥–æ–π –ø–∞—Ä—ã —Ñ–∏–≥—É—Ä: XM Xm YM Ym, –Ω—É–∂–Ω–æ —Å–ª–æ–∂–∏—Ç—å –∏—Ö –ø–æ–ø–∞—Ä–Ω–æ,
    # –≤—Ç–æ—Ä—É—é –¥–≤–æ–π–∫—É –ø–µ—Ä–µ–Ω–æ—Å–∏–º –≤–ø–µ—Ä—ë–¥ –∏ —Å–∫–ª–∞–¥—ã–≤–∞–µ–º —Å 16
    s/\(11*\) \(11*\) \(11*\) \(11*\)/1111111111111111\2\4 \1\3/g

    # –≤—ã—á–∏—Ç–∞–µ–º –≤—Ç–æ—Ä—É—é –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—É –∏–∑ –ø–µ—Ä–≤–æ–π
    s/\(11*\)\(1*\) \1/\2/g

    # –æ–±—ä–µ–¥–∏–Ω—è–µ–º —Ü–∏—Å–ª–∞ –¥–æ –æ–±–µ–∏–º —Ñ–∏–≥—É—Ä–∞–º
    s/[# ]//g

    # –≥—Ä—É–ø–ø–∏—Ä—É–µ–º —Å–æ—Ç–Ω–∏ –∏ —Ç—ã—Å—è—á–∏
    s/1111111111/H/g; s/HHHHHHHHHH/T/g

    # –≤—Å—Ç–∞–≤–ª—è–µ–º –¥–≤–æ–µ—Ç–æ—á–∏—è
    s/\(.\)\1*/&:/g
    # –µ—Å–ª–∏ –Ω–µ—Ç –µ–¥–∏–Ω–∏—Ü, –≤ –∫–æ–Ω–µ—Ü¬†‚Äî –µ—â—ë –æ–¥–Ω–æ –¥–≤–æ–µ—Ç–æ—á–∏–µ
    /1/ ! s/$/:/
    # –µ—Å–ª–∏ –Ω–µ—Ç —Å–æ—Ç–µ–Ω, —Ç–æ –¥–æ –µ–¥–∏–Ω–∏—Ü –∏–ª–∏ –ø–æ—Å–ª–µ–¥–Ω–µ–≥–æ –¥–≤–æ–µ—Ç–æ—á–∏—è¬†‚Äî –µ—â—ë –¥–≤–æ–µ—Ç–æ—á–∏–µ
    /H/ ! s/[1:]/:&/

    y/HT/11/

    G; s/\n/B /

    b @
}

# —Å—É–º–º–∏—Ä–æ–≤–∞–Ω–Ω–∏–µ —á–∏—Å–µ–ª –Ω–∞ —Å—Ç–µ–∫–µ, –ø–æ–∫–∞ –Ω–µ –≤—Å—Ç—Ä–µ—Ç–∏—Ç—Å—è —Å–ª–æ–≤–æ ARRAY
/@sum-array()/ {
    h
    /ARRAY.*/ {
        s///
        s/$/ ::::::S/

        :sum-array::shift
        /[1:][1:]*B/ {
            # —Å–ª–æ–∂–µ–Ω–∏–µ —Ä–∞–∑—Ä—è–¥–∞
            :sum-array::sum
            /11*B/ {
                s/\(11*\)B\(.*\)\(1*\)S/B\2\1\3S/
                s/:1111111111\(1*\)S/1:\1S/

                b sum-array::sum
            }

            # —Å–¥–≤–∏–≥ —Ä–∞–∑—Ä—è–¥–∞
            s/:B/B/g; s/:\(1*\)S/S \1:/

            b sum-array::shift
        }

        s/:\(1*\)S/S \1:/; s/[^1:]//g
        G; s/ARRAY/#&/; s/:\n.*#ARRAY */B /
    }

    b @
}

# –≤—ã—á–∏—Ç–∞–Ω–∏–µ —á–∏—Å–µ–ª –Ω–∞ —Å—Ç–µ–∫–µ –∏–∑ –ø–µ—Ä–≤–æ–≥–æ, –ø–æ–∫–∞ –Ω–µ –≤—Å—Ç—Ä–µ—Ç–∏—Ç—Å—è —Å–ª–æ–≤–æ ARRAY
/@sub-array()/ {
    / *ARRAY.*/ {
        h; s///
        # —É –ø–µ—Ä–≤–æ–≥–æ —á–∏—Å–ª–∞ –∑–∞–º–µ–Ω—è–µ–º –±—É–∫–≤—É, —á—Ç–æ–±—ã –æ—Ç–ª–∏—á–∞—Ç—å
        s/B */M /

        # —É –∫–∞–∂–¥–æ–≥–æ —á–∏—Å–ª–∞ –≤–ø–µ—Ä–µ–¥–∏ –¥–æ–ª–∂–µ–Ω –±—ã—Ç—å –æ–≥—Ä–∞–Ω–∏—á–∏—Ç–µ–ª—å
        s/^/:/; s/ / :/g

        # —É –ø–µ—Ä–≤–æ–≥–æ —á–∏—Å–ª–∞ —Å–Ω–∏–º–∞–µ–º —Å–∞–º—ã–π –º–ª–∞–¥—à–∏–π —Ä–∞–∑—Ä—è–¥
        s/:\(1*\)\(M.*\)/:\2 :\1#S/
        :sub-array::loop

        # —Ç–µ–ø–µ—Ä—å –ø—Ä–æ–π–¥—ë–º—Å—è –ø–æ –º–ª–∞–¥—à–∏–º —Ä–∞–∑—Ä—è–¥–∞–º –æ—Å—Ç–∞–≤—à–∏—Ö—Å—è —á–∏—Å–µ–ª
        :sub-array::minus
        /:\(11*\)\(B.*\) :\1\(1*\)#S/ {
            s//:\2 :\3#S/
            b sub-array::minus
        }

        # –º–ª–∞–¥—à–∏–µ —Ä–∞–∑—Ä—è–¥—ã –¥–ª—è –≤—ã—á–∏—Ç–∞–Ω–∏—è –æ—Å—Ç–∞–ª–∏—Å—å?
        /:11*B/ {
            # –ø–µ—Ä–µ–Ω–æ—Å–∏–º —Ä–∞–∑—Ä—è–¥—ã –≤—ã—á–∏—Ç–∞–µ–º–æ–≥–æ –Ω–∞ –º–ª–∞–¥—à–∏–π
            :sub-array::cy
            # –µ—Å–ª–∏ –ø–µ—Ä–µ–Ω–æ—Å–∏—Ç—å –Ω–µ—á–µ–≥–æ, —Ç–æ –≤—Å—ë, –ø–æ–ª—É—á–∞–µ—Ç—Å—è —á–∏—Å–ª–æ –º–µ–Ω—å—à–µ –Ω—É–ª—è,
            # –≤–æ–∑–≤—Ä–∞—â–∞–µ–º –Ω–æ–ª—å, –≤—ã—Ö–æ–¥–∏–º
            /1.*M/ ! {
                s/.*/:::B/
                b sub-array::end
            }

            s/\(.*\)1:\(.*M\)/\1:1111111111\2/

            /1M/ ! b sub-array::cy

            # –¥–æ–±–∞–≤–ª—è–µ–º –∫ –≤—ã—á–∏—Ç–∞–µ–º–æ–º—É
            s/:\(1*\)\(M.*\) \(:.*\)#S/:\2 \3\1#S/

            b sub-array::minus
        }

        # —Å—Ä–µ–∑–∞–µ–º —É –≤—Å–µ—Ö –ø–æ –ø—É—Å—Ç–æ–º—É —Ç–µ–ø–µ—Ä—å —Ä–∞–∑—Ä—è–¥—É, —É —Ç–µ—Ö, —É –∫–æ–≥–æ –∏—Ö –Ω–µ –æ—Å—Ç–∞–ª–æ—Å—å, —É–±–∏—Ä–∞–µ–º
        s/:\([BM]\)/\1/g; s/ :*B//g

        # –±–µ—Ä—ë–º —Å–ª–µ–¥—É—é—â–∏–π —Ä–∞–∑—Ä—è–¥
        s/:\(1*\)\(M.*\) \([^ ]*\)#S/:\2 :\1#S \3S/

        # –µ—Å–ª–∏ –æ—Å—Ç–∞–ª–æ—Å—å —á—Ç–æ –≤—ã—á–∏—Ç–∞—Ç—å, –≤—ã—á–∏—Ç–∞–µ–º
        /B/ b sub-array::loop

        # —É–±–∏—Ä–∞–µ–º –ª–∏—à–Ω–µ–µ, –Ω–æ—Ä–º–∞–ª–∏–∑–∏—Ä—É–µ–º
        s/[#MS ]//g; s/://

        :sub-array::end

        G; s/ARRAY/#&/; s/\n.*#ARRAY */B /
    }

    b @
}

# –≤—ã–±–æ—Ä —É–∫–∞–∑–∞–Ω–Ω–æ–π —Ñ–∏–≥—É—Ä—ã (–≤–µ—Ä–Ω—ë—Ç—Å—è –≤ –≤–∏–¥–µ —Å—Ç—Ä–æ–∫–∏)
# XYF__XYF__ –≥–¥–µ F¬†‚Äî –Ω–∞–∏–º–µ–Ω–æ–≤–∞–Ω–∏–µ —Ñ–∏–≥—É—Ä—ã, __¬†‚Äî –º–µ—Å—Ç–æ –ø–æ–¥ –ø–µ—Ä–µ–±–æ—Ä –ø–æ–∑–∏—Ü–∏–∏
/@select-figures(.)/ {
    h
    # —É–±–∏—Ä–∞–µ–º –∏–∑ –¥–∞–Ω–Ω—ã—Ö –≤—Å—ë –ª–∏—à–Ω–µ–µ, –ø–∞—Ä–∞–º–µ—Ç—Ä –ø–æ–º–µ—á–∞–µ–º –º–∞—Ä–∫–µ—Ä–æ–º
    s/@select-figures(\(.\))\(.*\)/\2 Selected:\1/
    s/.*Board://
    s/ .*Selected:/ Selected:/

    # –≤—ã–¥–µ–ª—è–µ–º –∏–∑ –¥–æ—Å–∫–∏ —Ç–æ, —á—Ç–æ —É–∫–∞–∑–∞–ª –ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª—å
    :select-figures::select
    /\([a-h][0-9]\)\(.\)\(.* Selected:\2\)/ {
        s//\3\1\2__/
        b select-figures::select
    }

    # —É–±–∏—Ä–∞–µ–º –º–∞—Ä–∫–µ—Ä –∏ –∏–∑—É–≤–µ—á–µ–Ω–Ω—É—é –¥–æ—Å–∫—É
    s/.*Selected:.//

    # –≤–æ–∑–≤—Ä–∞—â–∞–µ–º —Å—Ç–µ–∫ –Ω–∞–∑–∞–¥
    G; s/\n/END /
    b @
}

/@iter-knight()/ {
    # —É–±–∏—Ä–∞–µ–º –∫–æ–Ω—è, –∫–æ—Ç–æ—Ä—ã–π —Ö–æ–¥ –∑–∞–∫–æ–Ω—á–∏–ª
    s/^...XX//
    # –≤—ã—Ö–æ–¥–∏–º, –µ—Å–ª–∏ —Ö–æ–¥–∏—Ç—å –Ω–µ—á–µ–º
    /^END/ b @

    # –≤—ã–¥–µ–ª—è–µ–º –ø–µ—Ä–≤–æ–≥–æ –∫–æ–Ω—è
    h; s/\(.....\).*/\1/

    # –∫–æ–¥–∏—Ä–æ–≤–∫–∞ —Ö–æ–¥–æ–≤: __¬†‚Äî –Ω–µ –±—ã–ª —Å–¥–µ–ª–∞–Ω, XX ‚Äî —Å–¥–µ–ª–∞–Ω—ã –≤—Å–µ –≤–æ–∑–º–æ–∂–Ω—ã–µ
    # Left, Down, Up, Right, –ø–µ—Ä–≤—ã–º –ø–∏—à–µ—Ç—Å—è —Ö–æ–¥ –Ω–∞ –¥–≤–µ –∫–ª–µ—Ç–∫–∏, –Ω–∞–ø—Ä–∏–º–µ—Ä:
    # LU¬†‚Äî –≤–ª–µ–≤–æ –Ω–∞ –¥–≤–µ, –≤–≤–µ—Ä—Ö –Ω–∞ –æ–¥–Ω—É

    /__/ {
        s//LU/
        # —Å–¥–≤–∏–≥–∞–µ–º –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—É X-2, Y+1, 0¬†‚Äî –ø—Ä–∏–∑–Ω–∞–∫, —á—Ç–æ —Ö–æ–¥ –Ω–µ–≤–æ–∑–º–æ–∂–µ–Ω
        y/abcdefgh/00abcdef/
        y/12345678/23456780/

        b common::go
    }

    /LU/ {
        s//UL/
        # X-1, Y+2
        y/abcdefgh/0abcdefg/
        y/12345678/34567800/

        b common::go
    }

    /UL/ {
        s//UR/
        # X+1, Y+2
        y/abcdefgh/bcdefgh0/
        y/12345678/34567800/

        b common::go
    }

    /UR/ {
        s//RU/
        # X+2, Y+1
        y/abcdefgh/cdefgh00/
        y/12345678/23456780/

        b common::go
    }

    /RU/ {
        s//RD/
        # X+2, Y-1
        y/abcdefgh/cdefgh00/
        y/12345678/01234567/

        b common::go
    }

    /RD/ {
        s//DR/
        # X+1, Y-2
        y/abcdefgh/bcdefgh0/
        y/12345678/00123456/

        b common::go
    }

    /DR/ {
        s//DL/
        # X-1, Y-2
        y/abcdefgh/0abcdefg/
        y/12345678/00123456/

        b common::go
    }

    /DL/ {
        s//XX/
        # X-2, Y-1
        y/abcdefgh/00abcdef/
        y/12345678/01234567/

        b common::go
    }

    b common::go
}

# –∫–æ—Ä–æ–ª—å —Ö–æ–¥–∏—Ç –Ω–∞ –æ–¥–Ω—É –∫–ª–µ—Ç–∫—É –∫—É–¥–∞ —É–≥–æ–¥–Ω–æ              N
# –∫–æ–¥–∏—Ä–æ–≤–∫–∞ –ø–æ —Å—Ç–æ—Ä–æ–Ω–∞–º —Å–≤–µ—Ç–∞                        W   E
# __¬†‚Üí NN¬†‚Üí EN ‚Üí EE¬†‚Üí SE¬†‚Üí SS¬†‚Üí WS¬†‚Üí WW¬†‚Üí NW¬†‚Üí XX      S
/@iter-king()/ {
    # —É–±–∏—Ä–∞–µ–º –∫–æ—Ä–æ–ª—è, –∫–æ—Ç–æ—Ä—ã–π —Ö–æ–¥ –∑–∞–∫–æ–Ω—á–∏–ª
    s/^...XX//
    # –≤—ã—Ö–æ–¥–∏–º, –µ—Å–ª–∏ —Ö–æ–¥–∏—Ç—å –Ω–µ—á–µ–º
    /^END/ b @

    # –≤—ã–¥–µ–ª—è–µ–º –ø–µ—Ä–≤–æ–≥–æ (–∏ –µ–¥–∏–Ω—Å—Ç–≤–µ–Ω–Ω–æ–≥–æ) –∫–æ—Ä–æ–ª—è
    h; s/\(.....\).*/\1/

    # —Ç–µ–∫—É—â—É—é –≤—ã–±—Ä–∞–Ω–Ω—É—é –ø–æ–∑–∏—Ü–∏—é –º–µ–Ω—è–µ–º –Ω–∞ —Å–ª–µ–¥—É—é—â—É—é
    s/$/ __NNENEESESSWSWWNWXX/
    s/\(..\) \(.*\1\)\(..\)/\3 \2\3/; s/ .*//

    # –∑–∞–º–µ–Ω—è–µ–º –∫–æ–æ—Ä–¥–∏–Ω—Ç—ã, —Å–æ–≥–ª–∞—Å–Ω–æ –≤—ã–±—Ä–∞–Ω–Ω–æ–π –ø–æ–∑–∏—Ü–∏–∏

    # Y+1
    /N/ y/12345678/23456780/
    # Y-1
    /S/ y/12345678/01234567/
    # X-1
    /W/ y/abcdefgh/0abcdefg/
    # X+1
    /E/ y/abcdefgh/bcdefgh0/

    b common::go
}

# –ª–∞–¥—å—è, —Ö–æ–¥–∏—Ç –ø–æ –≤–µ—Ä—Ç–∏–∫–∞–ª–∏ –∏–ª–∏ –≥–æ—Ä–∏–∑–æ–Ω—Ç–∞–ª–∏ –Ω–∞ –ª—é–±–æ–µ –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ —Ö–æ–¥–æ–≤,   N
# –µ—Å–ª–∏ –Ω–∏–∫—Ç–æ –Ω–µ —Å—Ç–æ–∏—Ç –Ω–∞ –ø—É—Ç–∏                                          W   E
# —Ö–æ–¥–∏—Ç –Ω–∞—á–∏–Ω–∞–µ–º —Å —Ç–µ–∫—É—â–µ–π –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã –≤ —É–∫–∞–∑–∞–Ω–Ω—É—é —Å—Ç–æ—Ä–æ–Ω—É                S
/@iter-rook()/ {
    # —É–±–∏—Ä–∞–µ–º –ª–∞–¥—å—é, –∫–æ—Ç–æ—Ä–∞—è —Ö–æ–¥ –∑–∞–∫–æ–Ω—á–∏–ª–∞
    s/^...XX//
    # –≤—ã—Ö–æ–¥–∏–º, –µ—Å–ª–∏ —Ö–æ–¥–∏—Ç—å –Ω–µ—á–µ–º
    /^END/ b @

    # –≤—ã–¥–µ–ª—è–µ–º –ø–µ—Ä–≤—É—é –ª–∞–¥—å—é
    h; s/\(.....\).*/\1/

    # –ø–µ—Ä–≤–æ–µ –Ω–∞—à–µ –Ω–∞–ø—Ä–∞–≤–ª–µ–Ω–∏–µ¬†‚Äî –≤–æ—Å—Ç–æ–∫, –ø–æ—Ç–æ–º –∏–¥—ë–º –ø–æ —Å–ª–µ–¥—É—é—â–∏–º –Ω–∞–ø—Ä–∞–≤–ª–µ–Ω–∏—è–º
    /__/ s/\(\(.\).*\)__/\1E\2/
    /E0/ s/\(\(.\).*\)E./\1W\2/
    /W0/ s/\(.\(.\).*\)W./\1N\2/
    /N0/ s/\(.\(.\).*\)N./\1S\2/
    s/S0/XX/

    /E/ y/abcdefgh/bcdefgh0/
    /W/ y/abcdefgh/0abcdefg/
    
    /S/ y/12345678/01234567/
    /N/ y/12345678/23456780/

    # –ø–µ—Ä–µ–ø–∏—Å—ã–≤–∞–µ–º —Å–æ—Å—Ç–æ—è–Ω–∏–µ –≤ –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã –≤—ã–±—Ä–∞–Ω–Ω–æ–π —Ñ–∏–≥—É—Ä—ã, —Ç–∞–∫ –∫–∞–∫ —Ñ–∏–≥—É—Ä–∞ —Ö–æ–¥–∞ –ø—Ä–æ–ø–∞–¥—ë—Ç
    /[SN]/ s/\(.\).\(..\(.\)\)/\1\3\2/
    /[WE]/ s/.\(...\(.\)\)/\2\1/

    /[0X]/ ! {
        # –≤–æ–∑–≤—Ä–∞—â–∞–µ–º —Å—Ç–µ–∫, —É–±–∏—Ä–∞–µ–º –≤—Å—ë, —á—Ç–æ –∑–∞ –∏ –ø–µ—Ä–µ–¥ –¥–æ—Å–∫–æ–π –Ω–∞ —Å—Ç–µ–∫–µ
        s/$/#/; G; s/\n.*\(Board:[^ ]*\).*/\1/

        # –µ—Å–ª–∏ –Ω–∞ –≤—ã–±—Ä–∞–Ω–Ω–æ–π –ø–æ–∑–∏—Ü–∏–∏ –ø—Å–µ–≤–¥–æ–∫–æ—Ä–æ–ª—å, –±–æ–ª—å—à–µ –Ω–∏ –Ω–∞ —á—Ç–æ –Ω–µ –æ–±—Ä–∞—â–∞–µ–º –≤–Ω–∏–º–∞–Ω–∏—è
        /^\(..\).*\1z/ ! {
            # –ø—Ä–æ–≤–µ—Ä–∫–∞, –Ω–µ —Å—Ç–æ–∏—Ç –ª–∏ –Ω–∞ –≤—ã–±—Ä–∞–Ω–Ω–æ–π –ø–æ–∑–∏—Ü–∏–∏ —Å–≤–æ—è —Ñ–∏–≥—É—Ä–∞, –µ—Å–ª–∏ —Å—Ç–æ–∏—Ç, –ø—Ä–µ–∫—Ä–∞—â–∞–µ–º —Å–∫–∞–Ω —Å—Ä–∞–∑—É
            s/^\(..\)R\(.\).*\(\1[PQKINR]\).*/00R\20#\3/
            s/^\(..\)r\(.\).*\(\1[pqkinr]\).*/00r\20#\3/

            # –µ—Å–ª–∏ –∂–µ —Ç–∞–º —Å—Ç–æ–∏—Ç —á—É–∂–∞—è —Ñ–∏–≥—É—Ä–∞, —Ç–æ –¥–≤–∏–≥–∞—Ç—å—Å—è –º–æ–∂–Ω–æ, –∞ —Ö–æ–¥–∏—Ç—å¬†–∑–∞ –Ω–µ—ë ‚Äî –Ω–µ—Ç
            s/^\(..\)R\(.\).*\(\1[pqkinr]\).*/\1R\20#\3/
            s/^\(..\)r\(.\).*\(\1[PQKINR]\).*/\1r\20#\3/
        }
        s/#.*//
    }

    b common::go
}

# —Å–ª–æ–Ω (–æ—Ñ–∏—Ü–µ—Ä) —Ö–æ–¥–∏—Ç –ø–æ –¥–∏–∞–≥–æ–Ω–∞–ª–∏, –ø—Ä–∏ —É—Å–ª–æ–≤–∏–∏, —á—Ç–æ –Ω–∞ –ø—É—Ç–∏ –Ω–µ—Ç —Ñ–∏–≥—É—Ä
# —Ö–æ–¥–∏—Ç—å –Ω–∞—á–∏–Ω–∞–µ–º —Å —Ç–µ–∫—É—â–µ–≥–æ –º–µ—Å—Ç–∞, –æ–±–æ–∑–Ω–∞—á–µ–Ω–∏—è –Ω–∞–ø—Ä–∞–≤–ª–µ–Ω–∏–π: ‚Üò (v), ‚Üñ (^), ‚Üó (+), ‚Üô (-)
# –ø–æ–ø—ã—Ç–∫–∞ –∏—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å —Ç—É—Ç –Æ–Ω–∏–∫–æ–¥ –ø—Ä–∏–≤–µ–ª–∞ –∫ –ø–µ—Ä–µ–æ–¥–∏—á–µ—Å–∫–æ–º—É –≤—ã–ª–µ—Ç—É —É—Ç–∏–ª–∏—Ç—ã sed
# –æ–±–æ–∑–Ω–∞—á–µ–Ω–∏–µ —Ö–æ–¥–∞ –≤—ã–≥–ª—è–¥–∏—Ç —Ç–∞–∫: ‚Üô8¬†‚Äî –æ—Ç–æ—à–ª–∏ –æ—Ç —Ç–µ–∫—É—â–µ–π –ø–æ–∑–∏—Ü–∏–∏ –Ω–∞ 8 —à–∞–≥–æ–≤
/@iter-bishop()/ {
    # —É–±–∏—Ä–∞–µ–º —Å–ª–æ–Ω–∞, –∫–æ—Ç–æ—Ä—ã–π —Ö–æ–¥ –∑–∞–∫–æ–Ω—á–∏–ª
    s/^...XX//
    # –≤—ã—Ö–æ–¥–∏–º, –µ—Å–ª–∏ —Ö–æ–¥–∏—Ç—å –Ω–µ—á–µ–º
    /^END/ b @

    # –≤—ã–¥–µ–ª—è–µ–º –ø–µ—Ä–≤–æ–≥–æ —Å–ª–æ–Ω–∞
    h; s/\(.....\).*/\1/

    # –µ—Å–ª–∏ –Ω—É–∂–µ–Ω –ø–µ—Ä–µ—Ö–æ–¥ –Ω–∞ —Å–ª–µ–¥—É—é—â–µ–µ –Ω–∞–ø—Ä–∞–≤–ª–µ–Ω–∏–µ
    /^....[0_]/ {
        # —Ç–µ–∫—É—â—É—é –≤—ã–±—Ä–∞–Ω–Ω—É—é –ø–æ–∑–∏—Ü–∏—é –º–µ–Ω—è–µ–º –Ω–∞ —Å–ª–µ–¥—É—é—â—É—é
        s/$/ __v‚ë†v0^‚ë†^0+‚ë†+0-‚ë†-0XX/
        s/\(..\) \(.*\1\)\(..\)/\3 \2\3/; s/ .*//
        b iter-bishop::changed
    }
    # –∏–Ω–∞—á–µ –∏–¥—ë–º –ø–æ –≤—ã–±—Ä–∞–Ω–Ω–æ–º –Ω–∞–ø—Ä–∞–≤–ª–µ–Ω–∏—é –¥–∞–ª—å—à–µ
    y/‚ë†‚ë°‚ë¢‚ë£‚ë§‚ë•‚ë¶‚ëß/‚ë°‚ë¢‚ë£‚ë§‚ë•‚ë¶‚ëß0/
    :iter-bishop::changed

    # –ø–µ—Ä–µ–≤–µ–¥—ë–º –¥–µ—Å—è—Ç–∏—á–Ω–æ–µ —á–∏—Å–ª–æ –≤ –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ —Å—Ç—Ä–µ–ª–æ–∫, —Å–æ—Ö—Ä–∞–Ω–∏–≤ —Ç–µ–∫—É—â–µ–µ —Å–æ—Å—Ç–æ—è–Ω–∏–µ 
    H
    :iter-bishop::tobin
    /[0X]/ ! {
        y/‚ë†‚ë°‚ë¢‚ë£‚ë§‚ë•‚ë¶‚ëß/0‚ë†‚ë°‚ë¢‚ë£‚ë§‚ë•‚ë¶/
        s/.$/&‚Üí/
        b iter-bishop::tobin
    }

    :iter-bishop::minus
    # –≤—ã—á–∏—Å–ª—è–µ–º –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã
    /‚Üí/ {
        s///

        # X-1
        /[\-\^]/ y/abcdefgh/0abcdefg/
        # X+1
        /[+v]/ y/abcdefgh/bcdefgh0/
        # Y-1
        /[-v]/ y/12345678/01234567/
        # Y+1
        /[+\^]/ y/12345678/23456780/

        b iter-bishop::minus
    }

    # –≤–æ–∑—Ä–∞—â–∞–µ–º —Ç–æ —Å–æ—Å—Ç–æ—è–Ω–∏–µ, –∫–æ—Ç–æ—Ä–æ–µ –±—ã–ª–æ, —Å–µ–π—á–∞—Å —É –Ω–∞—Å: —Å—Ç–µ–∫, \n, –∏—Å—Ö–æ–¥–Ω–æ–µ —Å–æ—Å—Ç–æ—è–Ω–∏–µ, \n, –≤—ã—á–∏—Å–ª–µ–Ω–Ω–æ–µ
    # —Å –∏—Å–ø–æ—Ä—á–µ–Ω–Ω—ã–º –Ω–æ–º–µ—Ä–æ–º —Ö–æ–¥–∞

    # –≤—ã—á–∏—â–∞–µ–º –∏–∑ —Å—Ç–µ–∫–∞ –ª–∏—à–Ω–∏–µ –¥–∞–Ω–Ω—ã–µ¬†‚Äî –∏—Ö –æ—Ç–ø—Ä–∞–≤–ª—è–µ–º –≤ —Ö—Ä–∞–Ω–∏–ª–∏—â–µ
    H; x; s/\n/#/; h; s/#.*//

    # –º–µ–Ω—è–µ–º –º–µ—Å—Ç–∞–º–∏, —Ç–µ–ø–µ—Ä—å –≤ —Ö—Ä–∞–Ω–∏–ª–∏—â–µ —É –Ω–∞—Å —á–∏—Å—Ç—ã–π —Å—Ç–µ–∫, –∞ —É –Ω–∞—Å: –∏—Å—Ö–æ–¥–Ω–æ–µ \n –∏—Å–ø–æ—Ä—á–µ–Ω–Ω–æ–µ
    x; s/.*#//

    # –ø–µ—Ä–µ–Ω–æ—Å–∏–º –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã –∏–∑ –∏—Å–ø–æ—Ä—á–µ–Ω–Ω–æ–≥–æ –≤ –∏—Å—Ö–æ–¥–Ω–æ–µ (–≤ –∏—Å–ø–æ—Ä—á–µ–Ω–Ω–æ–º –º—ã –≤—ã—á–∏—Å–ª–∏–ª–∏ –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã —Ö–æ–¥–∞),
    # –∏—Å–ø–æ—Ä—á–µ–Ω–Ω–æ–µ —Å–æ—Å—Ç–æ—è–Ω–∏–µ —É–Ω–∏—á—Ç–æ–∂–∞–µ–º
    s/..\(.*\)\n\(..\).*/\2\1/

    /[0X]/ ! {
        # –≤–æ–∑–≤—Ä–∞—â–∞–µ–º —Å—Ç–µ–∫, —É–±–∏—Ä–∞–µ–º –≤—Å—ë, —á—Ç–æ –∑–∞ –∏ –ø–µ—Ä–µ–¥ –¥–æ—Å–∫–æ–π –Ω–∞ —Å—Ç–µ–∫–µ
        s/$/#/; G; s/\n.*\(Board:[^ ]*\).*/\1/
        # –µ—Å–ª–∏ –Ω–∞ –≤—ã–±—Ä–∞–Ω–Ω–æ–π –ø–æ–∑–∏—Ü–∏–∏ –ø—Å–µ–≤–¥–æ–∫–æ—Ä–æ–ª—å, –Ω–∏ –Ω–∞ —á—Ç–æ –Ω–µ –æ–±—Ä–∞—â–∞–µ–º –±–æ–ª—å—à–µ –≤–Ω–∏–º–∞–Ω–∏–µ
        /^\(..\).*\1z/ ! {
            # –ø—Ä–æ–≤–µ—Ä–∫–∞, –Ω–µ —Å—Ç–æ–∏—Ç –ª–∏ –Ω–∞ –≤—ã–±—Ä–∞–Ω–Ω–æ–π –ø–æ–∑–∏—Ü–∏–∏ —Å–≤–æ—è —Ñ–∏–≥—É—Ä–∞, –µ—Å–ª–∏ —Å—Ç–æ–∏—Ç, –ø—Ä–µ–∫—Ä–∞—â–∞–µ–º —Å–∫–∞–Ω —Å—Ä–∞–∑—É
            s/^\(..\)I\(.\).*\(\1[PQKINR]\).*/00I\20#\3/
            s/^\(..\)i\(.\).*\(\1[pqkinr]\).*/00i\20#\3/

            # –µ—Å–ª–∏ –∂–µ —Ç–∞–º —Å—Ç–æ–∏—Ç —á—É–∂–∞—è —Ñ–∏–≥—É—Ä–∞, —Ç–æ –¥–≤–∏–≥–∞—Ç—å—Å—è –º–æ–∂–Ω–æ, –∞ —Ö–æ–¥–∏—Ç—å¬†–∑–∞ –Ω–µ—ë ‚Äî –Ω–µ—Ç
            s/^\(..\)I\(.\).*\(\1[pqkinr]\).*/\1I\20#\3/
            s/^\(..\)i\(.\).*\(\1[PQKINR]\).*/\1i\20#\3/
        }
        s/#.*//
    }

    b common::go
}


# —á—ë—Ä–Ω—ã–µ –ø–µ—à–∫–∏ —É–º–µ—é—Ç —Ö–æ–¥–∏—Ç—å –ø–æ 4–º –Ω–∞–ø—Ä–∞–≤–ª–µ–Ω–∏—è–º:
# 1) –Ω–∞ 1 —Ö–æ–¥ (DO, Direction One)
# 2) –Ω–∞ 2 –¥–æ —Å–µ—Ä–µ–¥–∏–Ω—ã –¥–æ—Å–∫–∏, –µ—Å–ª–∏ –ø–æ–ª–µ –ø–µ—Ä–µ–¥ –Ω–µ–π –Ω–µ –∑–∞–Ω—è—Ç–æ (DT, Direction Two)
# 3) –≤–Ω–∏–∑ –≤–ª–µ–≤–æ, –µ—Å–ª–∏ —Ç–∞–º —á—É–∂–∞—è —Ñ–∏–≥—É—Ä–∞ (DL, Direction Left)
# 4) –≤–Ω–∏–∑ –≤–ø—Ä–∞–≤–æ, –µ—Å–ª–∏ —Ç–∞–º —á—É–∂–∞—è —Ñ–∏–≥—É—Ä–∞ (DR, Direction Right)
# –∫—Ä–æ–º–µ —Ç–æ–≥–æ, –ø–µ—à–∫–∞, –¥–æ—Å—Ç–∏–≥–∞—è –∫—Ä–∞—è –¥–æ—Å–∫–∏, –∏–º–µ–µ—Ç –ø—Ä–∞–≤–æ –ø—Ä–µ–≤—Ä–∞—Ç–∏—Ç—å—Å—è –≤ –ª—é–±—É—é —Ñ–∏–≥—É—Ä—É (–∫—Ä–æ–º–µ –∫–æ—Ä–æ–ª—è)
/@iter-pawn()/ {
    # —É–±–∏—Ä–∞–µ–º –ø–µ—à–∫—É, –∫–æ—Ç–æ—Ä–∞—è —Ö–æ–¥ –∑–∞–∫–æ–Ω—á–∏–ª–∞
    s/^...XX//
    # –≤—ã—Ö–æ–¥–∏–º, –µ—Å–ª–∏ —Ö–æ–¥–∏—Ç—å –Ω–µ—á–µ–º
    /^END/ b @

    # –≤—ã–¥–µ–ª—è–µ–º –ø–µ—Ä–≤—É—é –ø–µ—à–∫—É
    h; s/\(.....\).*/\1/

    # —Å–º–æ—Ç—Ä–∏–º, –µ—Å–ª–∏ —á—ë—Ä–Ω–∞—è –ø–µ—à–∫–∞ –Ω–∞ —Å–µ–¥—å–º–æ–π –≥–æ—Ä–∏–∑–æ–Ω—Ç–∞–ª–∏, –º–æ–∂–Ω–æ —Å–¥–µ–ª–∞—Ç—å –¥–ª–∏–Ω–Ω—ã–π —Ö–æ–¥, –∏–Ω–∞—á–µ –¥–µ–ª–∞–µ–º –∫–æ—Ä–æ—Ç–∫–∏–π
    /^\(.\)7P__/ {
        # —Å–æ–±—Å—Ç–≤–µ–Ω–Ω–æ, —Å–∞–º —Ö–æ–¥
        s//\15PDT/
        b iter-pawn::checkpiece
    }

    # —Å–º–æ—Ç—Ä–∏–º, –µ—Å–ª–∏ –±–µ–ª–∞—è –ø–µ—à–∫–∞ –Ω–∞ –≤—Ç–æ—Ä–æ–π –≥–æ—Ä–∏–∑–æ–Ω—Ç–∞–ª–∏, –º–æ–∂–Ω–æ —Å–¥–µ–ª–∞—Ç—å –¥–ª–∏–Ω–Ω—ã–π —Ö–æ–¥, –∏–Ω–∞—á–µ –¥–µ–ª–∞–µ–º –∫–æ—Ä–æ—Ç–∫–∏–π
    /^\(.\)2p__/ {
        # —Å–æ–±—Å—Ç–≤–µ–Ω–Ω–æ, —Å–∞–º —Ö–æ–¥
        s//\14pDT/
        b iter-pawn::checkpiece
    }

    # –∏–Ω–∞—á–µ, –¥–≤–∏–≥–∞–µ–º—Å—è –ø–æ –Ω–∞–ø—Ä–∞–≤–ª–µ–Ω–∏—è–º
    s/$/ __DODLDRXXDTDO/
    s/\(..\) \(.*\1\)\(..\)/\3 \2\3/; s/ .*//

    /D/ y/12345678/01234567/

    /[RL]/ {
        /L/ y/abcdefgh/0abcdefg/
        /R/ y/abcdefgh/bcdefgh0/
        # –¥–ª—è —ç—Ç–∏—Ö —Ö–æ–¥–æ–≤ –Ω—É–∂–Ω–∞ –æ–±—è–∑–∞—Ç–µ–ª—å–Ω–æ —á—É–∂–∞—è —Ñ–∏–≥—É—Ä–∞ –Ω–∞ –∫–ª–µ—Ç–∫–µ, –∫—É–¥–∞ —Ö–æ–¥–∏–º
        # –≤–æ–∑–≤—Ä–∞—â–∞–µ–º –¥–æ—Å–∫—É
        G; s/\n.*\(Board:[^ ]*\).*/ \1/

        # –ø—Ä–æ–≤–µ—Ä—è–µ–º, –µ—Å–ª–∏ –Ω–µ—Ç, —Ç–æ –∏ –Ω–µ —Ö–æ–¥–∏–º —Ç—É–¥–∞
        # z¬†‚Äî ¬´–ø—Ä–∏–∑—Ä–∞—á–Ω—ã–π –∫–æ—Ä–æ–ª—å¬ª, —Ñ–µ–π–∫–æ–≤–∞—è —Ñ–∏–≥—É—Ä–∞ –¥–ª—è –ø—Ä–æ–≤–µ—Ä–∫–∏ —à–∞—Ö–∞ –∏ –º–∞—Ç–∞
        /^\(..\)P.*\1[pqkinrz]/ ! s/^../00/
        /^\(..\)p.*\1[PQKINRz]/ ! s/^../00/

        # —É–±–∏—Ä–∞–µ–º –∑–∞ —Å–æ–±–æ–π –¥–æ—Å–∫—É
        s/ *Board:.*//

        b common::go
    }

    :iter-pawn::checkpiece
    # –ø–µ—à–∫–∞ –Ω–µ –º–æ–∂–µ—Ç —Ö–æ–¥–∏—Ç—å –ø—Ä—è–º–æ, –µ—Å–ª–∏ —Ç–∞–º —Å—Ç–æ–∏—Ç —Ñ–∏–≥—É—Ä–∞
    # –≤–æ–∑–≤—Ä–∞—â–∞–µ–º –¥–æ—Å–∫—É
    G; s/\n.*\(Board:[^ ]*\).*/ \1/

    # –µ—Å–ª–∏ –Ω–∞ –ø—É—Ç–∏ –ø—Å–µ–≤–¥–æ–∫–æ—Ä–æ–ª—å, –Ω–µ –æ–±—Ä–∞—â–∞–µ–º –Ω–∞ –Ω–µ–≥–æ –≤–Ω–∏–º–∞–Ω–∏—è
    /^\(..\).*\1z/ ! {
        # –ø—Ä–æ–≤–µ—Ä—è–µ–º, –µ—Å—Ç—å –ª–∏ —Ç–∞–º —Ñ–∏–≥—É—Ä–∞, –ª—é–±–∞—è, –µ—Å–ª–∏ –µ—Å—Ç—å, —Ö–æ–¥–∏—Ç—å –Ω–µ –º–æ–∂–µ–º
        /^\(..\).*\1[pqkinrPQKINR]/ {
            s/^../00/
        }
    }

    # —É–±–∏—Ä–∞–µ–º –∑–∞ —Å–æ–±–æ–π –¥–æ—Å–∫—É
    s/ *Board:.*//

    b common::go
}

# —Ñ–µ—Ä–∑—å (–∫–æ—Ä–æ–ª–µ–≤–∞) —Ö–æ–¥–∏—Ç –ø–æ –¥–∏–∞–≥–æ–Ω–∞–ª–∏, –≥–æ—Ä–∏–∑–æ–Ω—Ç–∞–ª–∏ –≤ –≤–µ—Ä—Ç–∏–∫–∞–ª–∏, –ø—Ä–∏ —É—Å–ª–æ–≤–∏–∏, —á—Ç–æ –Ω–∞ –ø—É—Ç–∏ –Ω–µ—Ç —Ñ–∏–≥—É—Ä
# —Ö–æ–¥–∏—Ç—å –Ω–∞—á–∏–Ω–∞–µ–º —Å —Ç–µ–∫—É—â–µ–≥–æ –º–µ—Å—Ç–∞, –æ–±–æ–∑–Ω–∞—á–µ–Ω–∏—è –Ω–∞–ø—Ä–∞–≤–ª–µ–Ω–∏–π: ‚Üò (v), ‚Üñ (^), ‚Üó (+), ‚Üô (-), ‚Üê (<),¬†‚Üí (>), ‚Üì (,), ‚Üë (')
# –ø–æ–ø—ã—Ç–∫–∞ –∏—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å —Ç—É—Ç –Æ–Ω–∏–∫–æ–¥ –ø—Ä–∏–≤–µ–ª–∞ –∫ –ø–µ—Ä–µ–æ–¥–∏—á–µ—Å–∫–æ–º—É –≤—ã–ª–µ—Ç—É —É—Ç–∏–ª–∏—Ç—ã sed
# –æ–±–æ–∑–Ω–∞—á–µ–Ω–∏–µ —Ö–æ–¥–∞ –≤—ã–≥–ª—è–¥–∏—Ç —Ç–∞–∫: ‚Üô8¬†‚Äî –æ—Ç–æ—à–ª–∏ –æ—Ç —Ç–µ–∫—É—â–µ–π –ø–æ–∑–∏—Ü–∏–∏ –Ω–∞ 8 —à–∞–≥–æ–≤
/@iter-queen()/ {
    # —É–±–∏—Ä–∞–µ–º —Å–ª–æ–Ω–∞, –∫–æ—Ç–æ—Ä—ã–π —Ö–æ–¥ –∑–∞–∫–æ–Ω—á–∏–ª
    s/^...XX//
    # –≤—ã—Ö–æ–¥–∏–º, –µ—Å–ª–∏ —Ö–æ–¥–∏—Ç—å –Ω–µ—á–µ–º
    /^END/ b @

    # –≤—ã–¥–µ–ª—è–µ–º –ø–µ—Ä–≤–æ–≥–æ —Å–ª–æ–Ω–∞
    h; s/\(.....\).*/\1/

    # –µ—Å–ª–∏ –Ω—É–∂–µ–Ω –ø–µ—Ä–µ—Ö–æ–¥ –Ω–∞ —Å–ª–µ–¥—É—é—â–µ–µ –Ω–∞–ø—Ä–∞–≤–ª–µ–Ω–∏–µ
    /^....[0_]/ {
        # —Ç–µ–∫—É—â—É—é –≤—ã–±—Ä–∞–Ω–Ω—É—é –ø–æ–∑–∏—Ü–∏—é –º–µ–Ω—è–µ–º –Ω–∞ —Å–ª–µ–¥—É—é—â—É—é
        s/$/ __v‚ë†v0^‚ë†^0+‚ë†+0-‚ë†-0<‚ë†<0>‚ë†>0,‚ë†,0'‚ë†'0XX/
        s/\(..\) \(.*\1\)\(..\)/\3 \2\3/; s/ .*//
        b iter-queen::changed
    }
    # –∏–Ω–∞—á–µ –∏–¥—ë–º –ø–æ –≤—ã–±—Ä–∞–Ω–Ω–æ–º –Ω–∞–ø—Ä–∞–≤–ª–µ–Ω–∏—é –¥–∞–ª—å—à–µ
    y/‚ë†‚ë°‚ë¢‚ë£‚ë§‚ë•‚ë¶‚ëß/‚ë°‚ë¢‚ë£‚ë§‚ë•‚ë¶‚ëß0/
    :iter-queen::changed

    # –ø–µ—Ä–µ–≤–µ–¥—ë–º –¥–µ—Å—è—Ç–∏—á–Ω–æ–µ —á–∏—Å–ª–æ –≤ –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ —Å—Ç—Ä–µ–ª–æ–∫, —Å–æ—Ö—Ä–∞–Ω–∏–≤ —Ç–µ–∫—É—â–µ–µ —Å–æ—Å—Ç–æ—è–Ω–∏–µ 
    H
    :iter-queen::tobin
    /[0X]/ ! {
        y/‚ë†‚ë°‚ë¢‚ë£‚ë§‚ë•‚ë¶‚ëß/0‚ë†‚ë°‚ë¢‚ë£‚ë§‚ë•‚ë¶/
        s/.$/&‚Üí/
        b iter-queen::tobin
    }

    :iter-queen::minus
    # –≤—ã—á–∏—Å–ª—è–µ–º –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã
    /‚Üí/ {
        s///

        # X-1
        /[\-\^<]/ y/abcdefgh/0abcdefg/
        # X+1
        /[+v>]/ y/abcdefgh/bcdefgh0/
        # Y-1
        /[-v,]/ y/12345678/01234567/
        # Y+1
        /[+\^\']/ y/12345678/23456780/

        b iter-queen::minus
    }

    # –≤–æ–∑—Ä–∞—â–∞–µ–º —Ç–æ —Å–æ—Å—Ç–æ—è–Ω–∏–µ, –∫–æ—Ç–æ—Ä–æ–µ –±—ã–ª–æ, —Å–µ–π—á–∞—Å —É –Ω–∞—Å: —Å—Ç–µ–∫, \n, –∏—Å—Ö–æ–¥–Ω–æ–µ —Å–æ—Å—Ç–æ—è–Ω–∏–µ, \n, –≤—ã—á–∏—Å–ª–µ–Ω–Ω–æ–µ
    # —Å –∏—Å–ø–æ—Ä—á–µ–Ω–Ω—ã–º –Ω–æ–º–µ—Ä–æ–º —Ö–æ–¥–∞

    # –≤—ã—á–∏—â–∞–µ–º –∏–∑ —Å—Ç–µ–∫–∞ –ª–∏—à–Ω–∏–µ –¥–∞–Ω–Ω—ã–µ¬†‚Äî –∏—Ö –æ—Ç–ø—Ä–∞–≤–ª—è–µ–º –≤ —Ö—Ä–∞–Ω–∏–ª–∏—â–µ
    H; x; s/\n/#/; h; s/#.*//

    # –º–µ–Ω—è–µ–º –º–µ—Å—Ç–∞–º–∏, —Ç–µ–ø–µ—Ä—å –≤ —Ö—Ä–∞–Ω–∏–ª–∏—â–µ —É –Ω–∞—Å —á–∏—Å—Ç—ã–π —Å—Ç–µ–∫, –∞ —É –Ω–∞—Å: –∏—Å—Ö–æ–¥–Ω–æ–µ \n –∏—Å–ø–æ—Ä—á–µ–Ω–Ω–æ–µ
    x; s/.*#//

    # –ø–µ—Ä–µ–Ω–æ—Å–∏–º –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã –∏–∑ –∏—Å–ø–æ—Ä—á–µ–Ω–Ω–æ–≥–æ –≤ –∏—Å—Ö–æ–¥–Ω–æ–µ (–≤ –∏—Å–ø–æ—Ä—á–µ–Ω–Ω–æ–º –º—ã –≤—ã—á–∏—Å–ª–∏–ª–∏ –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã —Ö–æ–¥–∞),
    # –∏—Å–ø–æ—Ä—á–µ–Ω–Ω–æ–µ —Å–æ—Å—Ç–æ—è–Ω–∏–µ —É–Ω–∏—á—Ç–æ–∂–∞–µ–º
    s/..\(.*\)\n\(..\).*/\2\1/

    /[0X]/ ! {
        # –≤–æ–∑–≤—Ä–∞—â–∞–µ–º —Å—Ç–µ–∫, —É–±–∏—Ä–∞–µ–º –≤—Å—ë, —á—Ç–æ –∑–∞ –∏ –ø–µ—Ä–µ–¥ –¥–æ—Å–∫–æ–π –Ω–∞ —Å—Ç–µ–∫–µ
        s/$/#/; G; s/\n.*\(Board:[^ ]*\).*/\1/

        # –µ—Å–ª–∏ –Ω–∞ —ç—Ç–æ–π –ø–æ–∑–∏—Ü–∏–∏ —Å—Ç–æ–∏—Ç –ø—Å–µ–≤–¥–æ—Ñ–∏–≥—É—Ä–∞, —à–ø–∞—Ä–∏–º –¥–∞–ª—å—à–µ
        /^\(..\)[Qq].*\1z/ ! {
            # –ø—Ä–æ–≤–µ—Ä–∫–∞, –Ω–µ —Å—Ç–æ–∏—Ç –ª–∏ –Ω–∞ –≤—ã–±—Ä–∞–Ω–Ω–æ–π –ø–æ–∑–∏—Ü–∏–∏ —Å–≤–æ—è —Ñ–∏–≥—É—Ä–∞, –µ—Å–ª–∏ —Å—Ç–æ–∏—Ç, –ø—Ä–µ–∫—Ä–∞—â–∞–µ–º —Å–∫–∞–Ω —Å—Ä–∞–∑—É
            s/^\(..\)Q\(.\).*\(\1[PQKINR]\).*/00Q\20#\3/
            s/^\(..\)q\(.\).*\(\1[pqkinr]\).*/00q\20#\3/

            # –µ—Å–ª–∏ –∂–µ —Ç–∞–º —Å—Ç–æ–∏—Ç —á—É–∂–∞—è —Ñ–∏–≥—É—Ä–∞, —Ç–æ –¥–≤–∏–≥–∞—Ç—å—Å—è –º–æ–∂–Ω–æ, –∞ —Ö–æ–¥–∏—Ç—å¬†–∑–∞ –Ω–µ—ë ‚Äî –Ω–µ—Ç
            s/^\(..\)Q\(.\).*\(\1[pqkinr]\).*/\1Q\20#\3/
            s/^\(..\)q\(.\).*\(\1[PQKINR]\).*/\1q\20#\3/
        }
        s/#.*//
    }

    b common::go
}

# –ø–µ—Ä–µ–º–µ—â–∞–µ–º –ø–æ–∑–∏—Ü–∏—é –∏ —Å—É–º–º—É –≤ –∫–æ–Ω–µ—Ü —Å—Ç–µ–∫–∞,
# –ø–µ—Ä–µ–∫–ª–∞–¥—ã–≤–∞–µ–º —Å—á—ë—Ç—á–∏–∫ –ø–æ–∑–∏—Ü–∏–∏ —Ñ–∏–≥—É—Ä—ã
/@store-iter()/ {
    # –µ—Å–ª–∏ —Ö–æ–¥–∏—Ç—å –±—ã–ª–æ –Ω–µ–ª—å–∑—è, –≤—ã—á–∏—â–∞–µ–º –º—É—Å–æ—Ä
    /^[^ ]*B  *0..../ {
        s///
        b @
    }

    # (–æ—Ü–µ–Ω–∫–∞ –ø–æ–∑–∏—Ü–∏–∏) (—Ñ–∏–≥—É—Ä–∞ —Ö–æ–¥–∞) —Å—á—ë—Ç—á–∏–∫ –ø–æ–∑–∏—Ü–∏–∏ (—Ç–µ–∫—É—â–∞—è —Ñ–∏–≥—É—Ä–∞) –≤—Å—ë –æ—Å—Ç–∞–ª—å–Ω–æ–µ ‚Üí
    # —Ç–µ–∫—É—â–∞—è —Ñ–∏–≥—É—Ä–∞, –≤—Å—ë –æ—Å—Ç–∞–ª—å–Ω–æ–µ, —Å—É–º–º–∞, —Ö–æ–¥ –æ—Ç–∫—É–¥–∞‚Üí—Ö–æ–¥ –∫—É–¥–∞
    s/\([^ ]*\) *\(...\)..\(...\)\([^ ]*END *.*\)/\3\4 \1(\3‚Üí\2)/
    b @
}

# –ø–µ—Ä–µ–º–µ—â–∞–µ–º –ø–æ–∑–∏—Ü–∏—é–≤ –∫–æ–Ω–µ—Ü —Å—Ç–µ–∫–∞ (–±–µ–∑ –≤–µ—Å–∞ —Ñ–∏–≥—É—Ä—ã)
# –ø–µ—Ä–µ–∫–ª–∞–¥—ã–≤–∞–µ–º —Å—á—ë—Ç—á–∏–∫ –ø–æ–∑–∏—Ü–∏–∏ —Ñ–∏–≥—É—Ä—ã
/@store-only-iter()/ {
    # –µ—Å–ª–∏ —Ö–æ–¥–∏—Ç—å –±—ã–ª–æ –Ω–µ–ª—å–∑—è, –≤—ã—á–∏—â–∞–µ–º –º—É—Å–æ—Ä
    /^0..../ {
        s///
        b @
    }

    # (—Ñ–∏–≥—É—Ä–∞ —Ö–æ–¥–∞) —Å—á—ë—Ç—á–∏–∫ –ø–æ–∑–∏—Ü–∏–∏ (—Ç–µ–∫—É—â–∞—è —Ñ–∏–≥—É—Ä–∞) –≤—Å—ë –æ—Å—Ç–∞–ª—å–Ω–æ–µ ‚Üí
    # —Ç–µ–∫—É—â–∞—è —Ñ–∏–≥—É—Ä–∞, –≤—Å—ë –æ—Å—Ç–∞–ª—å–Ω–æ–µ, Move:—Ö–æ–¥ –∫—É–¥–∞
    s/^\(...\)..\(...\)\([^ ]*END *.*\)/\2\3 Move:\1/
    b @
}

# –≤—ã—á–∏—Å–ª–µ–Ω–∏–µ –ª—É—á—à–µ–≥–æ —Ö–æ–¥–∞ –∏–∑ —É–∫–∞–∑–∞–Ω–Ω—ã—Ö
/@find-best-move()/ {
    # –µ—Å—Ç—å –ª–∏ –æ—Ü–µ–Ω–∫–∏ (–±–æ–ª—å—à–µ –æ–¥–Ω–æ–π –ø—Ä–∏—á—ë–º)?
    /[1:][1:]*B/ {
        h
        # —É–±–∏—Ä–∞–µ–º –ª–∏—à–Ω–µ–µ
        s//Moves:&/; s/.*Moves:/ /

        # –Ω–æ—Ä–º–∞–ª–∏–∑–∞—Ü–∏—è —á–∏—Å–ª–∞
        s/ / :::::/g; s/ :*\(1*:1*:1*:1*:1*:1*B\)/ \1/g

        y/B/:/

        :find-best-move::cut
        # —Å–º–æ—Ç—Ä–∏–º, –µ—Å—Ç—å –ª–∏ —á–∏—Å–ª–∞ —Å –Ω–µ–ø—É—Å—Ç—ã–º —Å—Ç–∞—Ä—à–∏–º —Ä–∞–∑—Ä—è–¥–æ–º
        / 1/ {
            # –µ—Å–ª–∏ –µ—Å—Ç—å, —Ç–æ —É–±–∏—Ä–∞–µ–º —Ç–µ, —É –∫–æ—Ç–æ—Ä—ã—Ö —Å—Ç—Ä–∞—à–∏–π —Ä–∞–∑—Ä—è–¥ –ø—É—Å—Ç–æ–π
            s/ :[^ ]*//g
            # —Ç–µ–ø–µ—Ä—å –æ—Ç—Ä–µ–∑–∞–µ–º —É –∫–∞–∂–¥–æ–≥–æ –ø–æ –æ–¥–Ω–æ–π —Ü–∏—Ñ—Ä–µ —Å—Ç–∞—Ä—à–µ–≥–æ —Ä–∞–∑—Ä—è–¥–∞
            s/ 1/ /g
            b find-best-move::cut
        }
        # –ø–µ—Ä–µ—Ö–æ–¥ —á–µ—Ä–µ–∑ —Ä–∞–∑—Ä—è–¥, –µ—Å—Ç—å –ª–∏ –µ—â—ë —á–∏—Å–ª–∞ —Å —Ä–∞–∑—Ä—è–¥–∞–º–∏?
        s/ :/ /g
        /:/ b find-best-move::cut

        # –µ—Å–ª–∏ –±—ã–ª–æ –Ω–µ—Å–∫–æ–ª—å–∫–æ –º–∞–∫—Å–∏–º—É–º–æ–≤, –æ—Å—Ç–∞–≤–ª—è–µ–º —Ç–æ–ª—å–∫–æ –ø–µ—Ä–≤—ã–π
        s/^ *\([^ ]*\).*/\1/
        # –≤–æ–∑–≤—Ä–∞—â–∞–µ–º –¥–∞–Ω–Ω—ã–µ –Ω–∞ –æ—Å–Ω–æ–≤–Ω–æ–π —Å—Ç–µ–∫
        G; s/\n/ /

        # –º–∞—Ä–∫–∏—Ä—É–µ–º –º–µ—Å—Ç–æ, –≥–¥–µ —É –Ω–∞—Å –Ω–∞—Ö–æ–¥—è—Ç—Å—è –æ—Ü–µ–Ω–∫–∏
        s/[1:][1:]*B/Moves: &/

        # —Ç–µ–ø–µ—Ä—å, –µ—Å–ª–∏ –Ω–∞ —Å—Ç–µ–∫–µ –∑–∞–ø–∏—Å—å –≤–∏–¥–∞ (XYF‚ÜíXYF), –¥–æ–ø–∏—Å—ã–≤–∞–µ–º –∫ –Ω–µ–π –æ—Ü–µ–Ω–∫—É
        s/^\(([^)]*\)\(.* \)\([1:][1:]*B\1\)/\3\2/

        # —É–±–∏—Ä–∞–µ–º –Ω–µ–Ω—É–∂–Ω—ã–µ —Ç–µ–ø–µ—Ä—å –æ—Ü–µ–Ω–∫–∏
        s/ *Moves:.*//
        # —Ç–µ–ø–µ—Ä—å –Ω–∞ —Å—Ç–µ–∫–µ –∑–∞–ø–∏—Å—å –≤–∏–¥–∞ Est(XYF‚ÜíXYF), –ª–∏–±–æ —Ç–∞–∫–æ–π –∑–∞–ø–∏—Å–∏ –≤–æ–æ–±—â–µ –Ω–µ—Ç (–µ—Å–ª–∏ –Ω–µ –±—ã–ª–æ –≤–æ–∑–º–æ–∂–Ω—ã—Ö —Ö–æ–¥–æ–≤ —É —Ñ–∏–≥—É—Ä—ã)
    }

    b @
}

# —Ö–æ–¥ —á—ë—Ä–Ω—ã—Ö, —Å–æ–≥–ª–∞—Å–Ω–æ –Ω–∞–π–¥–µ–Ω–Ω—ã–º –¥–∞–Ω–Ω—ã–º
/@move-black()/ {
    # —É–±–∏—Ä–∞–µ–º —Å –ø–æ–∑–∏—Ü–∏–∏ –∫—É–¥–∞ –±—É–¥–µ–º —Ö–æ–¥–∏—Ç—å —Ñ–∏–≥—É—Ä—É, —á—Ç–æ –±—ã —Ç–∞–º –Ω–µ —Å—Ç–æ—è–ª–æ, —É–¥–∞–ª—è–µ–º —ç—Ç–æ
    s/\([1:][1:]*B(\(..\)\(.\)‚Üí\(..\).).*Board:.*\)\4./\1\2¬†/
    # –º–µ–Ω—è–µ–º –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã —Ñ–∏–≥—É—Ä—ã, –∫–æ—Ç–æ—Ä–æ–π —Ö–æ–¥–∏–º
    s/[1:][1:]*B(\(..\)\(.\)‚Üí\(..\).) *\(.*Board:.*\)\1\2/\4\3\2/
    # –µ—Å–ª–∏ –ø–æ–ª—É—á–∏–ª–∞—Å—å –ø–µ—à–∫–∞ –Ω–∞ –ø–µ—Ä–≤–æ–π –≥–æ—Ä–∏–∑–æ–Ω—Ç–∞–ª–∏, –¥–µ–ª–∞–µ–º –∏–∑ –Ω–µ—ë —Ñ–µ—Ä–∑—è, –æ–Ω–∞ –º–æ–∂–µ—Ç –±—ã—Ç—å —Ç–æ–ª—å–∫–æ –æ–¥–Ω–∞
    s/\([a-h]1\)P/\1Q/

    b @
}

# —Å–æ–∑–¥–∞—ë–º –≤—Ç–æ—Ä—É—é –¥–æ—Å–∫—É –∏ —Ñ–µ–π–∫–æ–≤—ã—Ö –∫–æ—Ä–æ–ª–µ–π –Ω–∞ –Ω–µ–π, –≤ —Ç–µ—Ö –ø–æ–∑–∏—Ü–∏—è—Ö, –∫—É–¥–∞ –±—ã –∫–æ—Ä–æ–ª—å –º–æ–≥ —Å—Ö–æ–¥–∏—Ç—å
/@make-fake-kings()/ {
    # –∫–æ–ø–∏—Ä—É–µ–º –∫–æ—Ä–æ–ª—è —Å –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç–æ–π –≤ –∫–æ–Ω–µ—Ü
    s/\(...\)[^ ]* *\(.*\)/\2 King:\1__/

    h

    :make-fake-kings::loop
    /XX/ ! {
        # —É–±–∏—Ä–∞–µ–º –≤—Å—ë, –∫—Ä–æ–º–µ –∫–æ—Ä–æ–ª—è
        s/.*King:\(.....\).*/\1/

        # —Ç–µ–∫—É—â—É—é –≤—ã–±—Ä–∞–Ω–Ω—É—é –ø–æ–∑–∏—Ü–∏—é –º–µ–Ω—è–µ–º –Ω–∞ —Å–ª–µ–¥—É—é—â—É—é (HR¬†‚Äî —Ö–æ–¥ –Ω–∞ –º–µ—Å—Ç–µ)
        s/$/ __NNENEESESSWSWWNWXX/
        s/\(..\) [^ ]*\1\(..\).*/\2/

        # –∑–∞–º–µ–Ω—è–µ–º –∫–æ–æ—Ä–¥–∏–Ω—Ç—ã, —Å–æ–≥–ª–∞—Å–Ω–æ –≤—ã–±—Ä–∞–Ω–Ω–æ–π –ø–æ–∑–∏—Ü–∏–∏

        # Y+1
        /N/ y/12345678/23456780/
        # Y-1
        /S/ y/12345678/01234567/
        # X-1
        /W/ y/abcdefgh/0abcdefg/
        # X+1
        /E/ y/abcdefgh/bcdefgh0/

        H; g

        # –ø–µ—Ä–µ–ø–∏—Å—ã–≤–∞–µ–º –≤—ã–±—Ä–∞–Ω–Ω—É—é –ø–æ–∑–∏—Ü–∏—é –≤ –ø–æ–∑–∏—Ü–∏—é —Å–∞–º–æ–π –ø–µ—Ä–≤–æ–π —Ñ–∏–≥—É—Ä—ã¬†(—Ñ–∏–≥—É—Ä—ã –≥–¥–µ –∫–æ—Ä–æ–ª—å —Å—Ç–æ–∏—Ç —Å–µ–π—á–∞—Å)
        s/\(King:...\)..\(.*..[kK]\(..\)\)$/\1\3\2/

        # –Ω–∞ –≤—Ç–æ—Ä–æ–π –¥–æ—Å–∫–µ –≤ —É–∫–∞–∑–∞–Ω–Ω–æ–π –ø–æ–∑–∏—Ü–∏–∏ (–µ—Å–ª–∏ —Ç–∞–º –Ω–µ —Å—Ç–æ–∏—Ç —Å–≤–æ—è –∂–µ —Ñ–∏–≥—É—Ä–∞, –Ω—É–∂–Ω–æ –ø–æ—Å—Ç–∞–≤–∏—Ç—å
        # –ø—Å–µ–≤–¥–æ-–∫–æ—Ä–æ–ª—è (z)
        /.*\([^0][^0][kK]\)..$/ {
            x
            s//\1 &/

            # —Å—Ç–∞–≤–∏–º –≤ –∫–æ–Ω–µ—Ü –¥–æ—Å–∫–∏ –±–µ–ª–æ–≥–æ –ø—Å–µ–≤–¥–æ–∫–æ—Ä–æ–ª—è‚Ä¶
            /^\(..\)k \(Board:[^ ]*\)\1[^pqinrk]/ s/^\(..\) Board:[^ ]*/&\1z/
            # —á—ë—Ä–Ω–æ–≥–æ –ø—Å–µ–≤–æ–¥–æ–∫–æ—Ä–æ–ª—è
            /^\(..\)K \(Board:[^ ]*\)\1[^PQKINR]/ s/^\(..\) Board:[^ ]*/&\1z/
            # (–Ω–∞ –º–µ—Å—Ç–µ, –∫—É–¥–∞ –º—ã —è–∫–æ–±—ã –ø–æ–º–µ—â–∞–µ–º –ø—Å–µ–≤–¥–æ–∫–æ—Ä–æ–ª—è –Ω–µ –¥–æ–ª–∂–Ω–æ —Å—Ç–æ—è—Ç—å —Å–≤–æ–µ–π –∂–µ —Ñ–∏–≥—É—Ä—ã)
            # –µ—Å–ª–∏ –µ—ë —Ç–∞–º –Ω–µ –±—ã–ª–æ, —Ç–æ —Ç–µ–ø–µ—Ä—å –¥–≤–µ —è—á–µ–π–∫–∏ –∏–º–µ—é—Ç –æ–¥–Ω—É –∏ —Ç—É –∂–µ –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—É

            # —É–±–∏—Ä–∞–µ–º –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã –∫–æ—Ä–æ–ª—è –≤–≤–µ—Ä—Ö—É —Å—Ç–µ–∫–∞, –∫–æ—Ç–æ—Ä—ã–µ –º—ã –ø–æ—Å—Ç–∞–≤–∏–ª–∏ –¥–ª—è –ø—Ä–æ–≤–µ—Ä–æ–∫
            s/^... //

            x
        }

        b make-fake-kings::loop
    }

    # –∑–∞–º–µ–Ω—è–µ–º –ø–µ—Ä–µ–≤–æ–¥—ã —Å—Ç—Ä–æ–∫ –Ω–∞ –ø—Ä–æ–±–µ–ª—ã –≤ —Å—Ç–µ–∫–µ –∫–æ—Ä–æ–ª—è
    s/\n/ /g  

    b @
}

# –ø—Ä–æ–≤–µ—Ä–∫–∞ –º–∞—Ç–∞ –∏ —à–∞—Ö–∞ –∫–æ—Ä–æ–ª—é
/@check-mate()/ {
    # –≤–æ-–ø–µ—Ä–≤—ã—Ö, –≤—ã—á–∏—â–∞–µ–º –ø—Å–µ–≤–¥–æ–∫–æ—Ä–æ–ª–µ–π —Å –¥–æ—Å–∫–∏
    s/\(Board:[^ z]*\)..z[^ ]*/\1/

    # –≤–æ-–≤—Ç–æ—Ä—ã—Ö, —Å–æ—Ö—Ä–∞–Ω—è–µ–º —Å—Ç–µ–∫ –∏ –æ—Å—Ç–∞–≤–ª—è–µ–º —Ç–æ–ª—å–∫–æ —Ö–æ–¥—ã –∏ –≤–æ–∑–º–æ–∂–Ω—ã–µ —Ä–∞—Å–ø–æ–ª–æ–∂–µ–Ω–∏—è –∫–æ—Ä–æ–ª–µ–π
    h; s/.*King:\(..\(.\)\)/\2ing:\1/

    # –µ—Å—Ç—å –ª–∏ —É–≥—Ä–æ–∑–∞ –∫–æ—Ä–æ–ª—é?
    /[Kk]ing:\(..\).*Move:\1/ {
         # —ç—Ç–æ —É–∂–µ —à–∞—Ö, —Ç–µ–ø–µ—Ä—å –Ω–∞–¥–æ –ø–µ—Ä–µ–±—Ä–∞—Ç—å –≤—Å–µ –ø–æ–∑–∏—Ü–∏–∏ –∏ –ø–æ—Å–º–æ—Ç—Ä–µ—Ç—å –µ—Å—Ç—å –ª–∏ –∫—É–¥–∞ –∫–æ—Ä–æ–ª—é –¥–µ–≤–∞—Ç—å—Å—è,
         # –µ—Å–ª–∏ –Ω–µ—Ç, —ç—Ç–æ –º–∞—Ç

         :check-mate::loop
         /\(..\)[kK].. *\(.*\)Move:\1./ {
            s//\2/

            b check-mate::loop
         }

         /^k/ {
             # –ø–æ—Å–º–æ—Ç—Ä–∏–º, –æ—Å—Ç–∞–ª—Å—è –ª–∏ —Ö–æ–¥ –∫–æ—Ä–æ–ª—é
             /[^0][^0]k[^X0][^X0]/ ! {
                i\
                –í–∞–º —à–∞—Ö –∏ –º–∞—Ç, –≤—ã –ø—Ä–æ–∏–≥—Ä–∞–ª–∏

                q
             }
             i\
             –í–∞–º —à–∞—Ö

             b check-mate::cleanup
        }

        # –ø–æ—Å–º–æ—Ç—Ä–∏–º, –æ—Å—Ç–∞–ª—Å—è –ª–∏ —Ö–æ–¥ –∫–æ—Ä–æ–ª—é
        /[^0][^0]K[^X0][^X0]/ ! {
            i\
            –ú–Ω–µ —à–∞—Ö –∏ –º–∞—Ç, —è –ø—Ä–æ–∏–≥—Ä–∞–ª

            q
        }

        # –≤—ã—á–∏—â–∞–µ–º —Ö–æ–¥—ã –¥—Ä—É–≥–∏—Ö —Ñ–∏–≥—É—Ä
        s/ *Move:.*//

        # —Ñ–æ—Ä–º–∏—Ä—É–µ—Ç —Ö–æ–¥ –∫–æ—Ä–æ–ª—è –≤ –ø–µ—Ä–≤—É—é –ø–æ–ø–∞–≤—à—É—é—Å—è —Å—Ç–æ—Ä–æ–Ω—É
        s/.*\([^0 ][^0 ]K\)[^0 ][^0 ].*\([^ ][^ ]K\)XX.*/(\2‚Üí\1)/
        
        # –∫–ª–∞–¥—ë–º –µ–≥–æ –Ω–∞ –≤–µ—Ä—à–∏–Ω—É —Å—Ç–µ–∫–∞
        H; x; s/\(.*\)\n\(([^)]*)\)/1B\2 \1/; x

        b check-mate::cleanup
    }

    # –∫–ª–∞–¥—ë–º –º–µ—Ç–∫—É, —á—Ç–æ —Ö–æ–¥–æ–≤ –Ω–µ —Ç—Ä–µ–±—É–µ—Ç—Å—è
    x; s/^/END /; x

    :check-mate::cleanup
    # –≤–æ—Å—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º —Å—Ç–µ–∫ –∏ —É–±–∏—Ä–∞–µ–º —Å –Ω–µ–≥–æ –ª–∏—à–Ω–µ–µ

    g; s/ *King:.*//
    b @
}

# –≤–æ–∑–º–æ–∂–Ω–æ –Ω–∞ —Ö–æ–¥–µ —á—ë—Ä–Ω—ã—Ö –±–µ–ª–æ–≥–æ –∫–æ—Ä–æ–ª—è —Å—ä–µ–ª–∏?
/@check-white-king-exists()/ {
    /Board:[^ ]*k/ ! {
        i\
        –í–∞–º —à–∞—Ö –∏ –º–∞—Ç, –≤—ã –ø—Ä–æ–∏–≥—Ä–∞–ª–∏

        q  
    }

    b @
}

# –≤–æ–∑–º–æ–∂–Ω–æ –Ω–∞ —Ö–æ–¥–µ –±–µ–ª—ã—Ö —á—ë—Ä–Ω–æ–≥–æ –∫–æ—Ä–æ–ª—è —Å—ä–µ–ª–∏?
/@check-black-king-exists()/ {
    /Board:[^ ]*K/ ! {
        i\
        –ú–Ω–µ —à–∞—Ö –∏ –º–∞—Ç, —è –ø—Ä–æ–∏–≥—Ä–∞–ª

        q  
    }

    b @
}

/@ *$/ {
    q
}

b @

# —Ö–æ–∂–¥–µ–Ω–∏–µ —Ñ–∏–≥—É—Ä–æ–π
:common::go
# –≤–æ–∑–≤—Ä–∞—â–∞–µ–º —Å—Ç–µ–∫
G; s/\n//
# –ø–µ—Ä–µ–ø–∏—Å—ã–≤–∞–µ–º –∫—É–¥–∞ –º—ã —É–∂–µ —Ö–æ–¥–∏–ª–∏ –≤ —Ç–µ–∫—É—â—É—é —Ñ–∏–≥—É—Ä—É
# XYFPPXYF.. ‚Üí XYF__XYPP
s/\(...\)\(..\)\(...\)../\1__\3\2/

# –¥–∞–Ω–Ω—ã–µ –æ —Ñ–∏–≥—É—Ä–∞—Ö –∏ –¥–æ—Å–∫–∞, –æ—Å—Ç–∞–ª—å–Ω–æ–µ —É–±–∏—Ä–∞–µ–º
s/^\([^ ]*END\).*\(Board:[^ ]*\).*/\1 \2/

# –µ—Å–ª–∏ –Ω–∞ –ø—Ä–µ–¥–ø–æ–ª–∞–≥–∞–µ–º–æ–º –ø–æ–ª–µ –µ—Å—Ç—å —Ñ–∞–Ω—Ç–æ–º–Ω—ã–π –∫–æ—Ä–æ–ª—å, —Ç–æ –Ω–µ –æ–±—Ä–∞—â–∞–µ–º –≤–Ω–∏–º–∞–Ω–∏—è –Ω–∞ –æ—Å—Ç–∞–ª—å–Ω–æ–µ
/^\(..\).*\1z/ ! {
    # —Å–º–æ—Ç—Ä–∏–º –Ω–µ—Ç –ª–∏ –Ω–∞ –ø—Ä–µ–¥–ø–æ–ª–∞–≥–∞–µ–º–æ–º –ø–æ–ª–µ –Ω–∞—à–µ–π —Å–æ–±—Å—Ç–≤–µ–Ω–Ω–æ–π —Ñ–∏–≥—É—Ä—ã
    s/^\(..\)\([PQKINR].*\1[PQKINR]\)/00\2/
    s/^\(..\)\([pqkinr].*\1[pqkinr]\)/00\2/
}

# –µ—Å–ª–∏ –≤–æ –≤—Ç–æ—Ä–æ–π –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç–µ –Ω–æ–ª—å, —Å—Ç–∞–≤–∏–º –Ω–æ–ª—å –∏ –≤ –ø–µ—Ä–≤—É—é
s/^.0/00/

# –µ—Å–ª–∏ —Ö–æ–¥–∏—Ç—å —Å—é–¥–∞ –º–æ–∂–Ω–æ, —Ö–æ–¥–∏–º
/^0/ ! {
    # XY –§–∏–≥—É—Ä–∞ —Ö–æ–¥–∞ XY –§–∏–≥—É—Ä–∞ —Ç–µ–∫—É—â–∞—è, —Ö–æ–¥–∏–º –≤—Å–µ–≥–¥–∞ –Ω–∞ –¥–∞–ª—å–Ω–µ–π –¥–æ—Å–∫–µ
    # –º–µ–Ω—è–µ–º –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã —Ñ–∏–≥—É—Ä—ã, –∫–æ—Ç–æ—Ä–∞—è —Ö–æ–¥–∏—Ç
    s/\(\(...\)__\(...\).*Board:.*\)\3/\1\2/

    # –º–µ–Ω—è–µ–º –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã —Ç–æ–≥–æ –º–µ—Å—Ç–∞ –∫—É–¥–∞ —Ö–æ–¥–∏–º, —Å—ä–µ–¥–∞—è –ø–æ –ø—É—Ç–∏ —Ñ–∏–≥—É—Ä—É
    s/\(..\)\(.__\)\(..\)\(.*Board:.*\)\1./\1\2\3\4\3¬†/

    # –µ—Å–ª–∏ –ø–æ–ª—É—á–∏–ª–∞—Å—å –ø–µ—à–∫–∞ –Ω–∞ –ø–µ—Ä–≤–æ–π –≥–æ—Ä–∏–∑–æ–Ω—Ç–∞–ª–∏, –º–µ–Ω—è–µ–º –µ—ë –Ω–∞ —Ñ–µ—Ä–∑—è, –æ–Ω–∞ –º–æ–∂–µ—Ç –±—ã—Ç—å
    # —Ç–æ–ª—å–∫–æ –æ–¥–Ω–∞ –∏ —Ç–æ–ª—å–∫–æ –Ω–∞ –æ–¥–Ω–æ–π –¥–æ—Å–∫–µ
    s/\([a-h]1\)P/\1Q/
}

# —Å—Ç–µ–∫ –≤–æ–∑–≤—Ä–∞—â–∞–µ–º, —É–±–∏—Ä–∞—è —Å –Ω–µ–≥–æ –≤—Ç–æ—Ä–æ–π (–æ—Å—Ç–∞–≤—à–∏–π—Å—è) —Å—Ç–µ–∫ –≤—ã–¥–µ–ª–µ–Ω–Ω—ã—Ö —Ñ–∏–≥—É—Ä
G; s/\n[^ ]* */ /

# –º–µ–Ω—è–µ–º –Ω–∞—à—É –¥–æ–±–∞–≤–ª–µ–Ω–Ω—É—é –∏ –ø–æ—Å–ª–µ–¥–Ω—é—é –¥–æ—Å–∫—É –º–µ—Å—Ç–∞–º–∏
s/\(Board:[^ ]*\)\(.*\)\(Board:[^ ]*\)/\3\2\1/

b @
